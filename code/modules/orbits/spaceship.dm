//A thing for 'navigating' the current ship map up or down the gravity well.

#define HIGH_ORBIT 5
#define STANDARD_ORBIT 3
#define LOW_ORBIT 1

#define REQUIRED_POWER_AMOUNT 250000
#define AUTO_LOGOUT_TIME 1 MINUTES

#define AUTHORIZED 1
#define AUTHORIZED_PLUS 2

//so we can use the current orbit in other files
GLOBAL_VAR_INIT(current_orbit,STANDARD_ORBIT)

/obj/machinery/computer/navigation
	name = "\improper Helms computer"
	icon_state = "shuttlecomputer"
	screen_overlay = "shuttlecomputer_screen"
	density = TRUE
	anchored = TRUE
	idle_power_usage = 10
	req_access = list(ACCESS_MARINE_BRIDGE)
	interaction_flags = INTERACT_MACHINE_TGUI
	///boolean the spaceship it currently in the process of changing orbits
	var/changing_orbit = TRUE
	///boolean there is an authorized person logged into this console. TRUE = logged in authorized person
	var/authenticated = FALSE
	///boolean this machine is cut off from power and is sparking uncontrollably FALSE = everything fine
	var/shorted = FALSE
	///boolean this machine can be interacted with by the AI player. FALSE = can interact
	var/aidisabled = FALSE
	///timer id to prevent hardel from the varset call back
	var/timer_id

//-------------------------------------------
// Standard procs
//-------------------------------------------

/obj/machinery/computer/navigation/screwdriver_act(mob/living/user, obj/item/I)
	. = ..()

	if(.)
		return

	//keep this? make it hackable so regular marines can run?
	TOGGLE_BITFIELD(machine_stat, PANEL_OPEN)
	update_icon()
	to_chat(user, "线路已被[CHECK_BITFIELD(machine_stat, PANEL_OPEN) ? "exposed" : "unexposed"]")


/obj/machinery/computer/navigation/Initialize(mapload) //need anything special?
	. = ..()
	desc = "The navigation console for the [SSmapping.configs[SHIP_MAP].map_name]."
	timer_id = addtimer(VARSET_CALLBACK(src, changing_orbit, FALSE), 10 MINUTES, TIMER_STOPPABLE) //ship is still heading to area cant change orbit yet if your not at the planet

/obj/machinery/computer/navigation/Destroy()
	deltimer(timer_id)
	return ..()

/obj/machinery/computer/navigation/proc/reset(wire)
	switch(wire)
		if(WIRE_POWER)
			if(!wires.is_cut(WIRE_POWER))
				shorted = FALSE
				update_icon()
		if(WIRE_AI)
			if(!wires.is_cut(WIRE_AI))
				aidisabled = FALSE

//-------------------------------------------
// Special procs
//-------------------------------------------

/obj/machinery/computer/navigation/proc/get_power_amount()
	//check current powernet for total available power
	if(!powered())
		return 0

	var/area/here_we_are = get_area(src)
	var/obj/machinery/power/apc/myAPC = here_we_are.get_apc()

	return myAPC?.terminal?.powernet?.avail

/obj/machinery/computer/navigation/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "NavigationConsole", name)
		ui.open()

/obj/machinery/computer/navigation/ui_static_data(mob/user)
	. = list()
	.["ship_map_name"] = SSmapping.configs[SHIP_MAP]?.map_name || "Unknown Vessel"
	.["required_power"] = REQUIRED_POWER_AMOUNT
	.["high_orbit"] = HIGH_ORBIT
	.["standard_orbit"] = STANDARD_ORBIT
	.["low_orbit"] = LOW_ORBIT

/obj/machinery/computer/navigation/ui_data(mob/user)
	. = list()
	var/power_amount = get_power_amount()
	.["authenticated"] = authenticated
	.["current_orbit"] = GLOB.current_orbit
	.["power_amount"] = power_amount
	.["engines_ready"] = can_change_orbit(silent = TRUE)
	.["can_change_orbit"] = power_amount >= REQUIRED_POWER_AMOUNT
	.["changing_orbit"] = changing_orbit

/obj/machinery/computer/navigation/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	if(.)
		return

	var/mob/user = ui.user

	switch(action)
		if("login")
			if(isAI(user))
				authenticated = AUTHORIZED_PLUS
				addtimer(VARSET_CALLBACK(src, authenticated, FALSE), AUTO_LOGOUT_TIME)
				. = TRUE
				return
			if(!ishuman(user))
				return FALSE
			var/mob/living/carbon/human/human_user = user
			var/obj/item/card/id/id_card = human_user.get_active_held_item()
			if(!istype(id_card))
				id_card = human_user.wear_id
			if(!istype(id_card))
				return FALSE
			if(check_access(id_card))
				authenticated = AUTHORIZED
			if(ACCESS_MARINE_BRIDGE in id_card.access)
				authenticated = AUTHORIZED_PLUS
			if(authenticated)
				addtimer(VARSET_CALLBACK(src, authenticated, FALSE), AUTO_LOGOUT_TIME)
			. = TRUE

		if("logout")
			authenticated = FALSE
			. = TRUE

		if("UP")
			if(!authenticated)
				return FALSE
			if(get_power_amount() < REQUIRED_POWER_AMOUNT)
				to_chat(user, span_warning("电力储备不足, 无法改变轨道."))
				return FALSE
			do_orbit_checks("UP")
			TIMER_COOLDOWN_START(src, COOLDOWN_ORBIT_CHANGE, 1 MINUTES)
			. = TRUE

		if("DOWN")
			if(!authenticated)
				return FALSE
			if(get_power_amount() < REQUIRED_POWER_AMOUNT)
				to_chat(user, span_warning("电力储备不足, 无法改变轨道."))
				return FALSE
			do_orbit_checks("DOWN")
			TIMER_COOLDOWN_START(src, COOLDOWN_ORBIT_CHANGE, 1 MINUTES)
			. = TRUE


/obj/machinery/computer/navigation/proc/do_orbit_checks(direction)
	var/current_orbit = GLOB.current_orbit

	if(!can_change_orbit(current_orbit, direction))
		return

	message_admins("[ADMIN_TPMONTY(usr)] Has sent the ship [direction == "UP" ? "UPWARD" : "DOWNWARD"] in orbit")
	var/message = "Внимание. [direction == "UP" ? "Подъём" : "Снижение"] на другую орбиту через 10 секунд."
	minor_announce(message, title = "Смена Орбиты")
	addtimer(CALLBACK(src, PROC_REF(do_change_orbit), current_orbit, direction), 10 SECONDS)

/obj/machinery/computer/navigation/proc/can_change_orbit(current_orbit, direction, silent = FALSE)
	if(changing_orbit)
		if(!silent)
			to_chat(usr, span_warning("飞船正在改变轨道."))
		return FALSE
	if(direction == "UP" && current_orbit == HIGH_ORBIT)
		if(!silent)
			to_chat(usr, span_warning("飞船已处于最高轨道!"))
		return FALSE
	if(direction == "DOWN" && current_orbit == LOW_ORBIT)
		if(!silent)
			to_chat(usr, span_warning("飞船已处于最低轨道!"))
		return FALSE
	if(TIMER_COOLDOWN_RUNNING(src, COOLDOWN_ORBIT_CHANGE))
		if(!silent)
			to_chat(usr, span_warning("飞船正在根据之前的选择重新计算."))
		return FALSE
	return TRUE

/obj/machinery/computer/navigation/proc/do_change_orbit(current_orbit, direction)

	//chug that sweet sweet powernet juice, like 80% of total
	if(powered()) //do we still have power?
		idle_power_usage = 5000
		addtimer(VARSET_CALLBACK(src, idle_power_usage, 10), 5 MINUTES)
	else
		return
	changing_orbit = TRUE
	engine_shudder()

	var/message = "Переход на новую орбиту. Приготовьтесь к запуску двигателей и стабилизации."
	addtimer(CALLBACK(GLOBAL_PROC, GLOBAL_PROC_REF(minor_announce), message, "Смена Орбиты"), 290 SECONDS)
	addtimer(CALLBACK(src, PROC_REF(orbit_gets_changed), current_orbit, direction), 3 MINUTES)

/obj/machinery/computer/navigation/proc/orbit_gets_changed(current_orbit, direction)
	if(direction == "UP")
		if(current_orbit == LOW_ORBIT)
			current_orbit = STANDARD_ORBIT
		else
			current_orbit = HIGH_ORBIT

	if(direction == "DOWN")
		if(current_orbit == HIGH_ORBIT)
			current_orbit = STANDARD_ORBIT
		else
			current_orbit = LOW_ORBIT

	GLOB.current_orbit = current_orbit
	changing_orbit = FALSE
	engine_shudder()

//whole lotta shaking going on
/obj/machinery/computer/navigation/proc/engine_shudder()
	for(var/i in GLOB.alive_living_list) //knock down mobs
		var/mob/living/M = i
		if(!is_mainship_level(M.z))
			continue
		if(M.buckled)
			to_chat(M, span_warning("你被猛烈撞向[M.buckled]!"))
			shake_camera(M, 3, 1)
		else
			to_chat(M, span_warning("脚下的地板猛烈震动!"))
			shake_camera(M, 10, 1)
			M.Knockdown(0.3 SECONDS)
		CHECK_TICK
