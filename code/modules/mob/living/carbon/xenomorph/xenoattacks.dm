//There has to be a better way to define this shit. ~ Z
//can't equip anything
/mob/living/carbon/xenomorph/attack_ui(slot_id)
	return

/mob/living/carbon/xenomorph/attack_animal(mob/living/M as mob)
	if(isanimal(M))
		var/mob/living/simple_animal/S = M
		if(!S.melee_damage)
			M.do_attack_animation(src)
			S.emote("me", EMOTE_VISIBLE, "[S.friendly] [src]")
		else
			M.do_attack_animation(src, ATTACK_EFFECT_PUNCH)
			visible_message(span_danger("[S] [S.attacktext] [src]!"), null, null, 5)
			var/damage = S.melee_damage
			apply_damage(damage, BRUTE, blocked = MELEE)
			UPDATEHEALTH(src)
			log_combat(S, src, "attacked")

/mob/living/carbon/xenomorph/attack_hand(mob/living/user)
	. = ..()
	if(.)
		return

	if(!ishuman(user))
		return

	if(status_flags & INCORPOREAL) //Incorporeal xenos cannot attack
		return

	var/mob/living/carbon/human/H = user

	H.changeNext_move(7)
	switch(H.a_intent)

		if(INTENT_HELP)
			if(stat == DEAD)
				H.visible_message(span_warning("\The [H]戳了戳\the [src],但什么也没发生."), \
				span_warning("你戳了戳\the [src],但什么也没发生."), null, 5)
			else
				H.visible_message(span_notice("\The [H]抚摸了\the [src]."), \
					span_notice("你抚摸了\the [src]."), null, 5)

		if(INTENT_GRAB)
			if(H == src || anchored)
				return 0

			H.start_pulling(src)

		if(INTENT_DISARM, INTENT_HARM)
			var/datum/unarmed_attack/attack = H.species.unarmed
			if(!attack.is_usable(H))
				attack = H.species.secondary_unarmed
			if(!attack.is_usable(H))
				return FALSE

			if(!H.melee_damage)
				H.do_attack_animation(src)
				playsound(loc, attack.miss_sound, 25, TRUE)
				visible_message(span_danger("[H]试图[pick(attack.attack_verb)][src]!"), null, null, 5)
				return FALSE

			H.do_attack_animation(src, ATTACK_EFFECT_YELLOWPUNCH)
			playsound(loc, attack.attack_sound, 25, TRUE)
			visible_message(span_danger("[H] [pick(attack.attack_verb)] [src]!"), null, null, 5)
			apply_damage(melee_damage + attack.damage, BRUTE, blocked = MELEE, updating_health = TRUE)

//Hot hot Aliens on Aliens action.
//Actually just used for eating people.
/mob/living/carbon/xenomorph/attack_alien(mob/living/carbon/xenomorph/xeno_attacker, damage_amount = xeno_attacker.xeno_caste.melee_damage, damage_type = BRUTE, damage_flag = MELEE, effects = TRUE, armor_penetration = xeno_attacker.xeno_caste.melee_ap, isrightclick = FALSE)
	if(status_flags & INCORPOREAL || xeno_attacker.status_flags & INCORPOREAL) //Incorporeal xenos cannot attack or be attacked
		return

	if(src == xeno_attacker)
		return TRUE

	switch(xeno_attacker.a_intent)
		if(INTENT_HELP)
			if(on_fire)
				fire_stacks = max(fire_stacks - 1, 0)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				xeno_attacker.visible_message(span_danger("[xeno_attacker]试图扑灭[src]身上的火!"), \
					span_warning("我们试图扑灭[src]身上的火!"), null, 5)
				if(fire_stacks <= 0)
					xeno_attacker.visible_message(span_danger("[xeno_attacker]成功扑灭了[src]身上的火!"), \
						span_notice("我们扑灭了[src]身上的火."), null, 5)
					ExtinguishMob()
				return TRUE
			xeno_attacker.visible_message(span_notice("\The [xeno_attacker]用其镰刀般的手臂抚摸着\the [src]."), \
			span_notice("我们用镰刀般的手臂抚摸着\the [src]."), null, 5)
			return TRUE

		if(INTENT_DISARM)
			xeno_attacker.do_attack_animation(src, ATTACK_EFFECT_DISARM)
			playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
			if(!issamexenohive(xeno_attacker))
				return FALSE

			if(xeno_attacker.tier != XENO_TIER_FOUR && !(xeno_attacker.xeno_flags & XENO_LEADER))
				return FALSE

			if((isxenoqueen(src) || xeno_flags & XENO_LEADER) && !isxenoqueen(xeno_attacker))
				return FALSE

			xeno_attacker.visible_message("\The [xeno_attacker]将\the [src]推开!", \
				span_warning("你将\the [src]推开!"), null, 5)
			apply_effect(1 SECONDS, EFFECT_PARALYZE)
			return TRUE

		if(INTENT_GRAB)
			return attack_alien_grab(xeno_attacker)
		if(INTENT_HARM)
			return attack_alien_harm(xeno_attacker)

///////////////////////////////
//        MELTING FIRE       //
///////////////////////////////

/obj/fire/melting_fire
	name = "melting fire"
	desc = "摸起来很冷,却能灼烧."
	icon_state = "xeno_fire"
	flame_color = "purple"
	light_on = FALSE
	light_range = 0
	light_power = 0
	burn_ticks = 36
	burn_decay = 9
	/// The creator of this fire. Only really matters for pyrogens.
	var/mob/living/carbon/xenomorph/creator

/obj/fire/melting_fire/update_overlays()
	. = ..()
	. += emissive_appearance(icon, icon_state, src)

/obj/fire/melting_fire/affect_atom(atom/affected)
	if(isvehicle(affected))
		var/obj/vehicle/ghost_rider = affected
		ghost_rider.take_damage(burn_level / 2, BURN, ACID)
		return
	if(!ishuman(affected))
		return FALSE
	var/mob/living/carbon/human/human_affected = affected
	if(human_affected.stat == DEAD)
		return FALSE
	if(human_affected.status_flags & (INCORPOREAL|GODMODE))
		return
	if(human_affected.pass_flags & PASS_FIRE)
		return
	if(human_affected.soft_armor.getRating(FIRE) >= 100)
		to_chat(human_affected, span_warning("你未被火焰触及."))
		return
	handle_human(human_affected)

/// Handles everything that should be done to the human whom is affected by the fire.
/obj/fire/melting_fire/proc/handle_human(mob/living/carbon/human/affected_human)
	var/datum/status_effect/stacking/melting_fire/debuff = affected_human.has_status_effect(STATUS_EFFECT_MELTING_FIRE)
	if(debuff)
		debuff.add_stacks(PYROGEN_MELTING_FIRE_EFFECT_STACK, creator)
	else
		affected_human.apply_status_effect(STATUS_EFFECT_MELTING_FIRE, PYROGEN_MELTING_FIRE_EFFECT_STACK, creator)
	affected_human.take_overall_damage(PYROGEN_MELTING_FIRE_DAMAGE, BURN, FIRE, updating_health = TRUE, max_limbs = 2)
	return TRUE // For shattering fire

///////////////////////////////
//        SHATTERING FIRE    //
///////////////////////////////

/obj/fire/melting_fire/shattering
	name = "shattering fire"
	desc = "摸起来很冷,它会迅速在接触到的任何物体上蔓延裂纹."
	icon_state = "violet_1"
	flame_color = "violet"

/obj/fire/melting_fire/shattering/affect_atom(atom/affected)
	. = ..()
	if(.) // parent proc only returns true if it applies its effects to a human, so affected must be a human, ergo no type validation needed
		var/mob/living/carbon/human/victim = affected
		victim.apply_status_effect(STATUS_EFFECT_SHATTER, 3 SECONDS)

/obj/fire/melting_fire/melting_acid
	name = "melting acid fire"
	desc = "摸起来很冷,它以不止一种方式灼烧."
	icon_state = "green_1"
	flame_color = "green"

/obj/fire/melting_fire/melting_acid/handle_human(mob/living/carbon/human/affected_human)
	// In sum, it is acid-based and can't be put out like fire. Discounting armor, better than melting at very-low (1-2) and very-high (22-30).
	var/datum/status_effect/stacking/melting_acid/debuff = affected_human.has_status_effect(STATUS_EFFECT_MELTING_ACID)
	if(debuff)
		debuff.add_stacks(1)
	else
		affected_human.apply_status_effect(STATUS_EFFECT_MELTING_ACID, 1)
	affected_human.take_overall_damage(PYROGEN_MELTING_FIRE_DAMAGE, BURN, ACID, updating_health = TRUE, max_limbs = 2)
