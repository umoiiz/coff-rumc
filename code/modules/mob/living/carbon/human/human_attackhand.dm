/mob/living/carbon/human/attack_hand(mob/living/user)
	. = ..()
	if(.)
		return

	if(!ishuman(user))
		return

	var/mob/living/carbon/human/human_user = user

	if(user != src && !check_shields(COMBAT_TOUCH_ATTACK, human_user.melee_damage, MELEE))
		visible_message(span_danger("[user]试图触碰[src]!"), null, null, 5)
		return FALSE

	human_user.changeNext_move(7)
	switch(human_user.a_intent)
		if(INTENT_HELP)
			if(on_fire && human_user != src)
				fire_stacks = max(fire_stacks - 1, 0)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				human_user.visible_message(span_danger("[human_user]试图扑灭[src]身上的火!"), \
					span_warning("你试图扑灭[src]身上的火!"), null, 5)
				if(fire_stacks <= 0)
					human_user.visible_message(span_danger("[human_user]成功扑灭了[src]身上的火!"), \
						span_notice("你扑灭了[src]身上的火."), null, 5)
					ExtinguishMob()
				return TRUE

			if(istype(wear_mask, /obj/item/clothing/mask/facehugger) && human_user != src)
				human_user.stripPanelUnequip(wear_mask, src, SLOT_WEAR_MASK, BUSY_ICON_FACEHUGGER)
				return TRUE

			var/datum/status_effect/stacking/melting_fire/burning = has_status_effect(STATUS_EFFECT_MELTING_FIRE)
			if(burning)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				human_user.visible_message(span_danger("[human_user]试图扑灭[src]身上的火!"), \
				span_warning("你试图扑灭[src]身上的火!"), null, 5)
				burning.add_stacks(-2)
				if(QDELETED(burning))
					human_user.visible_message(span_danger("[human_user]成功扑灭了[src]身上的火!"), \
					span_notice("你扑灭了[src]身上的火."), null, 5)
				return TRUE

			if(health >= get_crit_threshold())
				help_shake_act(human_user)
				return TRUE

			if(HAS_TRAIT(src, TRAIT_UNDEFIBBABLE))
				to_chat(human_user, span_boldnotice("这个救不了了. 身体已经凉了."))
				return FALSE

			if(species?.species_flags & ROBOTIC_LIMBS)
				to_chat(human_user, span_boldnotice("你救不了这个, [p_they()] [p_have()]没有肺!"))
				return FALSE

			if((head && (head.inventory_flags & COVERMOUTH)) || (wear_mask && (wear_mask.inventory_flags & COVERMOUTH)))
				to_chat(human_user, span_boldnotice("摘下[p_their()]的面具!"))
				return FALSE

			if((human_user.head && (human_user.head.inventory_flags & COVERMOUTH)) || (human_user.wear_mask && (human_user.wear_mask.inventory_flags & COVERMOUTH)))
				to_chat(human_user, span_boldnotice("摘下你的面具!"))
				return FALSE

			//CPR
			if(human_user.do_actions)
				return TRUE

			human_user.visible_message(span_danger("[human_user]正在尝试对[src]进行心肺复苏!"), null, null, 4)

			if(!do_after(human_user, 4 SECONDS, NONE, src, BUSY_ICON_FRIENDLY, BUSY_ICON_MEDICAL))
				return TRUE

			if(health > get_death_threshold() && health < get_crit_threshold())
				var/suff = min(get_oxy_loss(), 5) //Pre-merge level, less healing, more prevention of dieing.
				adjust_oxy_loss(-suff)
				update_health()
				visible_message(span_warning("[human_user]对[src]进行了心肺复苏!"),
					span_boldnotice("你感到一股新鲜空气进入肺部. 感觉很好."),
					vision_distance = 3)
				to_chat(human_user, span_warning("至少每7秒重复一次."))
			else if(!HAS_TRAIT(src, TRAIT_UNDEFIBBABLE) && TIMER_COOLDOWN_FINISHED(src, COOLDOWN_CPR))
				TIMER_COOLDOWN_START(src, COOLDOWN_CPR, 7 SECONDS)
				dead_ticks -= 5
				visible_message(span_warning("[human_user]对[src]进行了心肺复苏!"), vision_distance = 3)
				to_chat(human_user, span_warning("病人多争取了一点时间. 每7秒重复一次."))
			else
				to_chat(human_user, span_warning("你未能救助[src]."))

			return TRUE

		if(INTENT_GRAB)
			if(human_user == src || anchored)
				return FALSE

			human_user.start_pulling(src)

			return TRUE

		if(INTENT_HARM)
			// See if they can attack, and which attacks to use.
			if(human_user == src && !human_user.do_self_harm)
				return FALSE
			var/datum/unarmed_attack/attack = human_user.species.unarmed
			if(!attack.is_usable(human_user))
				attack = human_user.species.secondary_unarmed
			if(!attack.is_usable(human_user))
				return FALSE

			var/attack_verb = pick(attack.attack_verb)
			//if you're lying/buckled, the miss chance is ignored anyway
			var/target_zone = get_zone_with_miss_chance(human_user.zone_selected, src, 10 - (human_user.skills.getRating(SKILL_CQC) - skills.getRating(SKILL_CQC)) * 5)

			if(!human_user.melee_damage || !target_zone)
				human_user.do_attack_animation(src)
				playsound(loc, attack.miss_sound, 25, TRUE)
				visible_message(span_danger("[human_user] [attack_verb]在[src], 但未命中!"), null, null, 5)
				log_combat(human_user, src, "[attack_verb]", "(missed)")
				if(!human_user.mind?.bypass_ff && !mind?.bypass_ff && human_user.faction == faction)
					var/turf/T = get_turf(src)
					log_ffattack("[key_name(human_user)] missed a punch against [key_name(src)] in [AREACOORD(T)].")
					msg_admin_ff("[ADMIN_TPMONTY(human_user)] missed a punch against [ADMIN_TPMONTY(src)] in [ADMIN_VERBOSEJMP(T)].")
				return FALSE

			human_user.do_attack_animation(src, ATTACK_EFFECT_YELLOWPUNCH)
			var/max_dmg = max(human_user.melee_damage + (human_user.skills.getRating(SKILL_CQC) * CQC_SKILL_DAMAGE_MOD), 3)
			var/damage = max_dmg
			if(!lying_angle)
				damage = rand(1, max_dmg)

			playsound(loc, attack.attack_sound, 25, TRUE)

			visible_message(span_danger("[human_user] [attack_verb] [src]!"), null, null, 5)
			var/list/hit_report = list()
			if(damage >= 4 && prob(25))
				visible_message(span_danger("[human_user]削弱了[src]!"), null, null, 5)
				apply_effect(modify_by_armor(6 SECONDS, MELEE, def_zone = target_zone), EFFECT_PARALYZE)
				hit_report += "(KO)"
			damage += attack.damage
			apply_damage(damage, BRUTE, target_zone, MELEE, attack.sharp, attack.edge, updating_health = TRUE)

			hit_report += "(RAW DMG: [damage])"

			log_combat(human_user, src, "[attack_verb]", "[hit_report.Join(" ")]")
			if(!human_user.mind?.bypass_ff && !mind?.bypass_ff && human_user.faction == faction)
				var/turf/T = get_turf(src)
				human_user.ff_check(damage, src)
				log_ffattack("[key_name(human_user)] punched [key_name(src)] in [AREACOORD(T)] [hit_report.Join(" ")].")
				msg_admin_ff("[ADMIN_TPMONTY(human_user)] punched [ADMIN_TPMONTY(src)] in [ADMIN_VERBOSEJMP(T)] [hit_report.Join(" ")].")

		if(INTENT_DISARM)

			human_user.do_attack_animation(src, ATTACK_EFFECT_DISARM)

			var/target_zone = ran_zone(human_user.zone_selected)

			//Accidental gun discharge
			if(human_user.skills.getRating(SKILL_CQC) < SKILL_CQC_TRAINED)
				if (istype(r_hand,/obj/item/weapon/gun) || istype(l_hand,/obj/item/weapon/gun))
					var/obj/item/weapon/gun/W = null
					var/chance = 0

					if (istype(l_hand,/obj/item/weapon/gun))
						W = l_hand
						chance = hand ? 40 : 20

					if (istype(r_hand,/obj/item/weapon/gun))
						W = r_hand
						chance = !hand ? 40 : 20

					if(prob(chance))
						visible_message(span_danger("[src]的[W.name]在挣扎中走火!"), null, null, 5)
						log_combat(human_user, src, "disarmed", "making their [W.name] go off")
						var/list/turfs = list()
						for(var/turf/T in view())
							turfs += T
						var/turf/target = pick(turfs)
						return W.afterattack(target,src)

			var/randn = rand(1, 100) + skills.getRating(SKILL_CQC) * CQC_SKILL_DISARM_MOD - human_user.skills.getRating(SKILL_CQC) * CQC_SKILL_DISARM_MOD

			if(randn <= 25)
				apply_effect(modify_by_armor(6 SECONDS, MELEE, def_zone = target_zone), EFFECT_PARALYZE)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				visible_message(span_danger("[human_user]将[src]推倒!"), null, null, 5)
				log_combat(human_user, src, "pushed")
				return

			if(randn <= 60)
				//BubbleWrap: Disarming breaks a pull
				if(pulling)
					visible_message(span_danger("[human_user]打破了[src]对[pulling]的抓握!"), null, null, 5)
					stop_pulling()
				else
					drop_held_item()
					visible_message(span_danger("[human_user]缴了[src]的械!"), null, null, 5)
				playsound(loc, 'sound/weapons/thudswoosh.ogg', 25, 1, 7)
				log_combat(user, src, "disarmed")
				return


			playsound(loc, 'sound/weapons/punchmiss.ogg', 25, 1, 7)
			visible_message(span_danger("[human_user]试图缴[src]的械!"), null, null, 5)
			log_combat(human_user, src, "missed a disarm")

/mob/living/carbon/human/proc/afterattack(atom/target, mob/living/user, inrange, params)
	return


/mob/living/carbon/human/help_shake_act(mob/living/carbon/M)
	if(src == M)
		if(holo_card_color) //if we have a triage holocard printed on us, we remove it.
			holo_card_color = null
			visible_message(span_notice("[src]取下了[p_them()]自己的全息卡."),
				span_notice("你取下了自己的全息卡."), null, 3)
			return


		check_self_for_injuries()
		return

	return ..()


/mob/living/carbon/human/proc/check_self_for_injuries()
	var/list/final_msg = list()
	balloon_alert_to_viewers("检查[p_them()]自己.", "你检查自己")
	final_msg += span_notice("<b>你检查自己是否受伤.</b>")

	for(var/datum/limb/org in limbs)
		var/status = ""
		var/treat = ""
		var/brutedamage = org.brute_dam
		var/burndamage = org.burn_dam
		var/brute_treated = org.is_bandaged()
		var/burn_treated = org.is_salved()

		switch(brutedamage)
			if(1 to 20)
				status += "bruised"
			if(20 to 40)
				status += "battered"
			if(40 to 60)
				status += "mangled"
			if(60 to 100)
				status += "brutalized"
			if(100 to INFINITY)
				status += "mutilated"

		if((org.limb_status & LIMB_BLEEDING) && (brutedamage > 0 && burndamage > 0))
			status += ", bleeding"
		else if((org.limb_status & LIMB_BLEEDING) && (brutedamage > 0 || burndamage > 0))
			status += " and bleeding"
		else if(org.limb_status & LIMB_BLEEDING)
			status += "bleeding"



		if(brutedamage > 0 && burndamage > 0)
			status += " and "

		switch(burndamage)
			if(1 to 20)
				status += "numb"
			if(20 to 40)
				status += "blistered"
			if(40 to 60)
				status += "peeling away"
			if(60 to 100)
				status += "searing away"
			if(100 to INFINITY)
				status += "burnt to a crisp"

		if(!status)
			status = "OK"

		if(org.limb_status & LIMB_SPLINTED)
			status += " <b>(SPLINTED)</b>"
		if(org.limb_status & LIMB_STABILIZED)
			status += " <b>(STABILIZED)</b>"
		if(org.limb_status & LIMB_NECROTIZED)
			status = "rotting"
		if(org.limb_status & LIMB_DESTROYED)
			status = "MISSING!"

		if(brute_treated && brutedamage > 0)
			treat = "(Bandaged"
			if(burn_treated && burndamage > 0)
				treat += " and Salved)"
			else
				treat += ")"
		else if(burn_treated && burndamage > 0)
			treat += "(Salved)"
		var/msg = "My [org.display_name] is [status]. [treat]"
		final_msg += status=="OK" ? span_notice(msg) : span_alert (msg)


	switch(staminaloss)
		if(1 to 30)
			final_msg += span_info("你感到疲惫.")
		if(30 to 60)
			final_msg += span_info("你感到相当疲倦.")
		if(60 to 90)
			final_msg += span_info("你相当疲惫.")
		if(90 to INFINITY)
			final_msg += span_info("你完全精疲力竭.")

	switch(oxyloss)
		if(1 to 15)
			final_msg += span_info("你感到有点喘不过气.")
		if(15 to 30)
			final_msg += span_info("你呼吸困难.")
		if(30 to 49)
			final_msg += span_info("你因缺氧而开始头晕.")

	switch(toxloss)
		if(1 to 5)
			final_msg += span_info("你的身体微微刺痛.")
		if(6 to 10)
			final_msg += span_info("你全身有点疼.")
		if(11 to 15)
			final_msg += span_info("你全身都疼.")
		if(15 to 25.99)
			final_msg += span_info("你全身疼得厉害.")
		if(26 to INFINITY)
			final_msg += span_info("你全身疼痛, 快把你逼疯了!")

	switch(germ_level)
		if(0 to 19)
			final_msg += span_info("你[pick("free of grime", "pristine", "freshly laundered")].")
		if(20 to 79)
			final_msg += span_info(pick("You've got some grime on you.", "You're a bit dirty."))
		if(80 to 150)
			final_msg += span_info(pick("You're not far off filthy.", "You're pretty dirty.", "There's still one or two clean spots left on you."))
		else
			final_msg += span_info(pick("There's a full layer of dirt covering you. Maybe it'll work as camo?", "You could go for a shower.", "You've reached a more complete understanding of grime."))

	to_chat(src, custom_boxed_message("blue_box", final_msg.Join("\n")))
