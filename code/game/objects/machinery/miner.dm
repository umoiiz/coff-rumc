#define MINER_RUNNING 0
#define MINER_SMALL_DAMAGE 1
#define MINER_MEDIUM_DAMAGE 2
#define MINER_DESTROYED 3
#define MINER_LIGHT_RUNNING 8
#define MINER_LIGHT_SDAMAGE 4
#define MINER_LIGHT_MDAMAGE 2
#define MINER_LIGHT_DESTROYED 0
#define MINER_PASSIVE 9
#define MINER_AUTOMATED "mining computer"
#define MINER_RESISTANT "reinforced components"
#define MINER_OVERCLOCKED "high-efficiency drill"

#define PHORON_CRATE_SELL_AMOUNT 150
#define PLATINUM_CRATE_SELL_AMOUNT 300
#define PHORON_DROPSHIP_BONUS_AMOUNT 15
#define PLATINUM_DROPSHIP_BONUS_AMOUNT 30

///Resource generator that produces a certain material that can be repaired by marines and attacked by xenos, Intended as an objective for marines to play towards to get more req gear
/obj/machinery/miner
	name = "\improper Nanotrasen phoron mining well"
	desc = "顶级的Nanotrasen研究钻机,自带出口模块,用于大量开采phoron.出售这些钻机开采的phoron将获得可观的利润..."
	icon = 'icons/obj/mining_drill.dmi'
	density = TRUE
	icon_state = "mining_drill_active"
	anchored = TRUE
	coverage = 30
	layer = ABOVE_MOB_LAYER
	resistance_flags = RESIST_ALL | DROPSHIP_IMMUNE
	allow_pass_flags = PASS_PROJECTILE|PASS_AIR
	faction = FACTION_TERRAGOV
	///How many sheets of material we have stored
	var/stored_mineral = 0
	///Current status of the miner
	var/miner_status = MINER_RUNNING
	///Tracks how many ticks have passed since we last added a sheet of material
	var/add_tick = 0
	///How many times we neeed to tick for a resource to be created, in this case this is 2* the specified amount
	var/required_ticks = 70  //make one crate every 140 seconds
	///The mineral type that's produced
	var/mineral_value = PHORON_CRATE_SELL_AMOUNT
	///Applies the actual bonus points for the dropship for each sale
	var/dropship_bonus = PHORON_DROPSHIP_BONUS_AMOUNT
	///Health for the miner we use because changing obj_integrity is apparently bad
	var/miner_integrity = 100
	///Max health of the miner
	var/max_miner_integrity = 100
	///What type of upgrade it has installed , used to change the icon of the miner.
	var/miner_upgrade_type
	var/obj/machinery/camera/miner/camera

	var/passive_mode = FALSE
	var/start_time = 0
	var/passive_duration = 15 MINUTES

/obj/machinery/miner/damaged //mapping and all that shebang
	miner_status = MINER_DESTROYED
	icon_state = "mining_drill_error"

/obj/machinery/miner/damaged/init_marker()
	return //Marker will be set by itself once processing pauses when it detects this miner is broke.

/obj/machinery/miner/damaged/platinum
	name = "\improper Nanotrasen platinum mining well"
	desc = "一种Nanotrasen铂金钻机,带有内置出口模块.产出比其phoron对应型号更有价值的材料."
	mineral_value = PLATINUM_CRATE_SELL_AMOUNT
	dropship_bonus = PLATINUM_DROPSHIP_BONUS_AMOUNT

/obj/machinery/miner/Initialize(mapload)
	. = ..()
	start_time = world.time
	init_marker()
	start_processing()
	RegisterSignal(SSdcs, COMSIG_GLOB_DROPSHIP_HIJACKED, PROC_REF(disable_on_hijack))
	camera = new /obj/machinery/camera/miner(src)

/**
 * This proc is called during Initialize() and should be used to initially setup the minimap marker of a functional miner.
 * * For a miner starting broken, it should be overridden and immediately return instead, as broken miners will automatically set their minimap marker during their first process()
 **/
/obj/machinery/miner/proc/init_marker()
	var/marker_icon = "miner_[mineral_value >= PLATINUM_CRATE_SELL_AMOUNT ? "platinum" : "phoron"]_on"
	SSminimaps.add_marker(src, MINIMAP_FLAG_ALL, image('icons/UI_icons/map_blips.dmi', null, marker_icon, MINIMAP_BLIPS_LAYER))

/obj/machinery/miner/update_icon_state()
	. = ..()
	if (miner_status == MINER_PASSIVE)
		icon_state = "mining_drill_passive"
		return
	switch(miner_status)
		if(MINER_RUNNING)
			icon_state = "mining_drill_active_[miner_upgrade_type]"
			set_light(MINER_LIGHT_RUNNING, MINER_LIGHT_RUNNING)
		if(MINER_SMALL_DAMAGE)
			icon_state = "mining_drill_braced_[miner_upgrade_type]"
			set_light(MINER_LIGHT_SDAMAGE, MINER_LIGHT_SDAMAGE)
		if(MINER_MEDIUM_DAMAGE)
			icon_state = "mining_drill_[miner_upgrade_type]"
			set_light(MINER_LIGHT_MDAMAGE, MINER_LIGHT_MDAMAGE)
		if(MINER_DESTROYED)
			icon_state = "mining_drill_error_[miner_upgrade_type]"
			set_light(MINER_LIGHT_DESTROYED, MINER_LIGHT_DESTROYED)

/// Called whenever someone attacks the miner with a object which is considered a upgrade.The object needs to have a uptype var.
/obj/machinery/miner/proc/attempt_upgrade(obj/item/minerupgrade/upgrade, mob/user, params)
	if(miner_upgrade_type)
		to_chat(user, span_info("[src]的模块插槽已被[miner_upgrade_type]占用."))
		return FALSE
	if(user.skills.getRating(SKILL_ENGINEER) < SKILL_ENGINEER_ENGI)
		user.visible_message(span_notice("[user]笨手笨脚地摸索着如何在[src]上安装模块."),
		span_notice("你笨手笨脚地摸索着如何在[src]上安装模块."))
		var/fumbling_time = 15 SECONDS - 2 SECONDS * user.skills.getRating(SKILL_ENGINEER)
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_UNSKILLED))
			return FALSE
	user.visible_message(span_notice("[user]开始将模块安装到[src]的插槽上."))
	to_chat(user, span_info("你开始在采矿机上安装[upgrade]."))
	if(!do_after(user, 15 SECONDS, NONE, src, BUSY_ICON_BUILD))
		return FALSE
	switch(upgrade.uptype)
		if(MINER_RESISTANT)
			max_miner_integrity = 300
			miner_integrity = 300
		if(MINER_OVERCLOCKED)
			required_ticks = 60
		if(MINER_AUTOMATED)
			if(stored_mineral)
				SSpoints.supply_points[faction] += mineral_value * stored_mineral
				SSpoints.dropship_points += dropship_bonus * stored_mineral
				GLOB.round_statistics.points_from_mining += mineral_value * stored_mineral
				do_sparks(5, TRUE, src)
				playsound(loc,'sound/effects/phasein.ogg', 50, FALSE)
				say("Ore shipment has been sold for [mineral_value * stored_mineral] points.")
				stored_mineral = 0
				start_processing()
	miner_upgrade_type = upgrade.uptype
	user.visible_message(span_notice("[user]将[miner_upgrade_type]安装到[src]上!"))
	qdel(upgrade)
	playsound(loc,'sound/items/screwdriver.ogg', 25, TRUE)
	update_icon()

/obj/machinery/miner/attackby(obj/item/I,mob/user,params)
	. = ..()
	if(.)
		return
	if(istype(I, /obj/item/minerupgrade))
		var/obj/item/minerupgrade/upgrade = I
		if(!(miner_status == MINER_RUNNING))
			to_chat(user, span_info("[src]的模块插槽似乎被螺栓固定住了."))
			return FALSE
		attempt_upgrade(upgrade,user)

/obj/machinery/miner/welder_act(mob/living/user, obj/item/I)
	. = ..()
	var/obj/item/tool/weldingtool/weldingtool = I
	if(miner_status == MINER_RUNNING && miner_upgrade_type)
		if(!weldingtool.remove_fuel(2, user))
			to_chat(user, span_info("你需要更多焊接燃料来完成此任务!"))
			return FALSE
		to_chat(user, span_info("你开始从采矿机上卸下[miner_upgrade_type]!"))
		user.visible_message(span_notice("[user]开始从采矿机上拆卸[miner_upgrade_type]."))
		var/fumbling_time = 30 SECONDS - 5 SECONDS * user.skills.getRating(SKILL_ENGINEER)
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_BUILD, extra_checks = CALLBACK(weldingtool, /obj/item/tool/weldingtool/proc/isOn)))
			return FALSE
		user.visible_message(span_notice("[user]从采矿机上拆下了[miner_upgrade_type]!"))
		var/obj/item/upgrade
		switch(miner_upgrade_type)
			if(MINER_RESISTANT)
				upgrade = new /obj/item/minerupgrade/reinforcement
				if(miner_integrity < max_miner_integrity)
					miner_integrity = round(miner_integrity/3)
					set_miner_status()
				else
					miner_integrity = initial(miner_integrity)
				max_miner_integrity = initial(max_miner_integrity)
			if(MINER_OVERCLOCKED)
				upgrade = new /obj/item/minerupgrade/overclock
				required_ticks = initial(required_ticks)
			if(MINER_AUTOMATED)
				upgrade = new /obj/item/minerupgrade/automatic
		upgrade.forceMove(user.loc)
		miner_upgrade_type = null
		update_icon()
	if(miner_status != MINER_DESTROYED)
		return
	if(!weldingtool.remove_fuel(1, user))
		to_chat(user, span_warning("你需要更多焊接燃料来完成此任务."))
		return FALSE
	if(user.skills.getRating(SKILL_ENGINEER) < SKILL_ENGINEER_ENGI)
		user.visible_message(span_notice("[user]笨手笨脚地摸索着[src]的内部结构."),
		span_notice("你笨手笨脚地摸索着[src]的内部结构."))
		var/fumbling_time = 10 SECONDS - 2 SECONDS * user.skills.getRating(SKILL_ENGINEER)
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_UNSKILLED, extra_checks = CALLBACK(weldingtool, TYPE_PROC_REF(/obj/item/tool/weldingtool, isOn))))
			return FALSE
	user.visible_message(span_notice("[user]开始焊接[src]的内部损伤."),
	span_notice("你开始焊接[src]的内部损伤."))
	if(!I.use_tool(src, user, 20 SECONDS, 2, 25, null, BUSY_ICON_BUILD))
		return
	if(miner_status != MINER_DESTROYED)
		return FALSE
	miner_integrity = 0.33 * max_miner_integrity
	set_miner_status()
	user.visible_message(span_notice("[user]焊接了[src]的内部损伤."),
	span_notice("你焊接了[src]的内部损伤."))
	record_miner_repair(user)
	return TRUE

/obj/machinery/miner/wirecutter_act(mob/living/user, obj/item/I)
	if(miner_status != MINER_MEDIUM_DAMAGE)
		return
	if(user.skills.getRating(SKILL_ENGINEER) < SKILL_ENGINEER_ENGI)
		user.visible_message(span_notice("[user]笨手笨脚地摸索着[src]的线路."),
		span_notice("你笨手笨脚地摸索着[src]的线路."))
		var/fumbling_time = 10 SECONDS - 2 SECONDS * user.skills.getRating(SKILL_ENGINEER)
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_UNSKILLED))
			return FALSE
	playsound(loc, 'sound/items/wirecutter.ogg', 25, TRUE)
	user.visible_message(span_notice("[user]开始固定[src]的线路."),
	span_notice("你开始固定[src]的线路."))
	if(!do_after(user, 120, NONE, src, BUSY_ICON_BUILD))
		return FALSE
	if(miner_status != MINER_MEDIUM_DAMAGE)
		return FALSE
	playsound(loc, 'sound/items/wirecutter.ogg', 25, TRUE)
	miner_integrity = 0.66 * max_miner_integrity
	set_miner_status()
	user.visible_message(span_notice("[user]固定了[src]的线路."),
	span_notice("你固定了[src]的线路."))
	record_miner_repair(user)
	return TRUE

/obj/machinery/miner/wrench_act(mob/living/user, obj/item/I)
	if(miner_status != MINER_SMALL_DAMAGE)
		return
	if(user.skills.getRating(SKILL_ENGINEER) < SKILL_ENGINEER_ENGI)
		user.visible_message(span_notice("[user]笨手笨脚地摸索着[src]的管道和外壳."),
		span_notice("你笨手笨脚地摸索着[src]的管道和外壳."))
		var/fumbling_time = 10 SECONDS - 2 SECONDS * user.skills.getRating(SKILL_ENGINEER)
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_UNSKILLED))
			return FALSE
	playsound(loc, 'sound/items/ratchet.ogg', 25, TRUE)
	user.visible_message(span_notice("[user]开始修理[src]的管道和外壳."),
	span_notice("你开始修理[src]的管道和外壳."))
	if(!do_after(user, 150, NONE, src, BUSY_ICON_BUILD))
		return FALSE
	if(miner_status != MINER_SMALL_DAMAGE)
		return FALSE
	playsound(loc, 'sound/items/ratchet.ogg', 25, TRUE)
	miner_integrity = max_miner_integrity
	passive_mode = FALSE
	start_time = world.time
	set_miner_status()
	user.visible_message(span_notice("[user]修理了[src]的管道和外壳."),
	span_notice("你修理了[src]的管道和外壳."))

	var/list/target_hive = GLOB.alive_xeno_list_hive[XENO_HIVE_NORMAL]
	if(target_hive && target_hive.len)
		for(var/mob/living/carbon/xenomorph/X in target_hive)
			to_chat(X, span_xenoannounce("我们感应到一处采矿井已被修复!"))

	start_processing()
	faction = user.faction
	record_miner_repair(user)
	return TRUE

/obj/machinery/miner/examine(mob/user)
	. = ..()
	if(!ishuman(user) && !isobserver(user))
		return
	if(!miner_upgrade_type)
		. += span_info("[src]的模块插槽似乎是空的,可以安装一个升级.")
	else
		. += span_info("[src]的模块插槽已被[miner_upgrade_type]占用.")

	switch(miner_status)
		if(MINER_DESTROYED)
			. += span_info("它严重受损, 你可以看到内部结构.</span>\n<span class='info'>使用喷灯, 然后是线切割器, 最后是扳手来修理它.")
		if(MINER_MEDIUM_DAMAGE)
			. += span_info("它受损了, 有断裂的电线悬挂在外面.</span>\n<span class='info'>使用线切割器, 然后是扳手来修理它.")
		if(MINER_SMALL_DAMAGE)
			. += span_info("它轻微受损, 你可以看到一些凹痕和松动的管道.</span>\n<span class='info'>使用扳手来修理它.")
		if(MINER_RUNNING)
			. += span_info("[src]的存储模块显示[stored_mineral]个板条箱已准备好出口.")
		if(MINER_PASSIVE)
			. += span_info("[src]进入了深层采矿模式.")

/obj/machinery/miner/attack_hand(mob/living/user)
	if(miner_status == MINER_PASSIVE)
		to_chat(user, span_warning("[src]已进入深层采矿模式, 它处于无敌且自动化的状态"))
		return
	if(miner_status != MINER_RUNNING)
		to_chat(user, span_warning("[src]受损了!"))
		return
	if(miner_upgrade_type == MINER_AUTOMATED)
		to_chat(user, span_warning("[src]已自动化!"))
		return
	if(!stored_mineral)
		to_chat(user, span_warning("[src]尚未准备好生产货物!"))
		return

	SSpoints.supply_points[faction] += mineral_value * stored_mineral
	SSpoints.dropship_points += dropship_bonus * stored_mineral
	GLOB.round_statistics.points_from_mining += mineral_value * stored_mineral
	do_sparks(5, TRUE, src)
	playsound(loc,'sound/effects/phasein.ogg', 50, FALSE)
	say("Ore shipment has been sold for [mineral_value * stored_mineral] points.")
	stored_mineral = 0
	start_processing()

/obj/machinery/miner/process()
	if(!passive_mode && world.time - start_time >= passive_duration)
		if(stored_mineral > 0)
			SSpoints.supply_points[faction] += mineral_value * stored_mineral
			SSpoints.dropship_points += dropship_bonus * stored_mineral
			GLOB.round_statistics.points_from_mining += mineral_value * stored_mineral
			do_sparks(5, TRUE, src)
			playsound(loc,'sound/effects/phasein.ogg', 50, FALSE)
			say("Ore shipment has been sold for [mineral_value * stored_mineral] points.")
			stored_mineral = 0

		if(miner_upgrade_type)
			var/turf/spawn_turf = get_step(src, SOUTH)
			switch(miner_upgrade_type)
				if(MINER_RESISTANT)
					new /obj/item/minerupgrade/reinforcement(spawn_turf)
					max_miner_integrity = initial(max_miner_integrity)
					if(miner_integrity > max_miner_integrity)
						miner_integrity = max_miner_integrity
				if(MINER_OVERCLOCKED)
					new /obj/item/minerupgrade/overclock(spawn_turf)
					required_ticks = initial(required_ticks)
				if(MINER_AUTOMATED)
					new /obj/item/minerupgrade/automatic(spawn_turf)
			miner_upgrade_type = null
			visible_message(span_notice("[src]在进入深层采矿模式时弹出了已安装的模块."))

		passive_mode = TRUE
		miner_status = MINER_PASSIVE
		update_icon()
		SSminimaps.remove_marker(src)
		var/marker_icon = "miner_[mineral_value >= PLATINUM_CRATE_SELL_AMOUNT ? "platinum" : "phoron"]_passive"
		SSminimaps.add_marker(src, MINIMAP_FLAG_ALL, image('icons/UI_icons/map_blips.dmi', null, marker_icon, MINIMAP_BLIPS_LAYER))

	if(miner_status != MINER_RUNNING && miner_status != MINER_PASSIVE)
		stop_processing()
		SSminimaps.remove_marker(src)
		var/marker_icon = "miner_[mineral_value >= PLATINUM_CRATE_SELL_AMOUNT ? "platinum" : "phoron"]_off"
		SSminimaps.add_marker(src, MINIMAP_FLAG_ALL, image('icons/UI_icons/map_blips.dmi', null, marker_icon, MINIMAP_BLIPS_LAYER))
		return

	if(add_tick >= required_ticks)
		if(miner_upgrade_type == MINER_AUTOMATED || passive_mode)
			for(var/direction in GLOB.cardinals)
				if(!isopenturf(get_step(loc, direction))) //Must be open on one side to operate
					continue

				var/actual_mineral_value = passive_mode ? round(mineral_value / 3) : mineral_value
				var/actual_dropship_bonus = passive_mode ? round(dropship_bonus / 3) : dropship_bonus

				SSpoints.supply_points[faction] += actual_mineral_value
				SSpoints.dropship_points += actual_dropship_bonus
				GLOB.round_statistics.points_from_mining += actual_mineral_value
				do_sparks(5, TRUE, src)
				playsound(loc,'sound/effects/phasein.ogg', 50, FALSE)
				say("Ore shipment has been sold for [actual_mineral_value] points.")
				add_tick = 0
				return
			playsound(loc,'sound/machines/buzz-two.ogg', 35, FALSE)
			add_tick = 0
			return
		stored_mineral += 1
		add_tick = 0
	if(stored_mineral >= 8 && !passive_mode) //Stores 8 boxes worth of minerals
		stop_processing()
	else
		add_tick += 1

/obj/machinery/miner/attack_alien(mob/living/carbon/xenomorph/xeno_attacker, damage_amount = xeno_attacker.xeno_caste.melee_damage, damage_type = BRUTE, damage_flag = MELEE, effects = TRUE, armor_penetration = xeno_attacker.xeno_caste.melee_ap, isrightclick = FALSE)
	if(miner_status == MINER_PASSIVE)
		to_chat(xeno_attacker, span_warning("钻机已进入深层采矿模式并且处于无敌状态!"))
		return
	if(xeno_attacker.status_flags & INCORPOREAL) //Incorporeal xenos cannot attack physically.
		return
	if(miner_upgrade_type == MINER_RESISTANT && !(xeno_attacker.mob_size == MOB_SIZE_BIG || xeno_attacker.xeno_caste.caste_flags & CASTE_IS_STRONG))
		xeno_attacker.visible_message(span_notice("[xeno_attacker]的爪子从[src]的强化装甲上弹开了."),
		span_notice("我们无法砍穿[src]的强化装甲!"))
		return
	while(miner_status != MINER_DESTROYED)
		if(xeno_attacker.do_actions)
			return balloon_alert(xeno_attacker, "忙碌")
		if(!do_after(xeno_attacker, 1.5 SECONDS, NONE, src, BUSY_ICON_DANGER, BUSY_ICON_HOSTILE))
			return
		xeno_attacker.do_attack_animation(src, ATTACK_EFFECT_CLAW)
		xeno_attacker.visible_message(span_danger("[xeno_attacker]劈砍\the [src]!"), \
		span_danger("我们劈砍\the [src]!"), null, 5)
		playsound(loc, SFX_ALIEN_CLAW_METAL, 25, TRUE)
		miner_integrity -= 25
		set_miner_status()
		if(miner_status == MINER_DESTROYED)
			if(miner_upgrade_type)
				switch(miner_upgrade_type)
					if(MINER_RESISTANT)
						max_miner_integrity = initial(max_miner_integrity)
					if(MINER_OVERCLOCKED)
						required_ticks = initial(required_ticks)
				xeno_attacker.visible_message(span_danger("[xeno_attacker]在此过程中摧毁了\the [miner_upgrade_type]升级!"), \
				span_danger("我们在此过程中摧毁了\the [miner_upgrade_type]升级!"), null, 5)
				miner_upgrade_type = null
				update_icon()
			if(xeno_attacker.client)
				var/datum/personal_statistics/personal_statistics = GLOB.personal_statistics_list[xeno_attacker.ckey]
				personal_statistics.miner_sabotages_performed++

/obj/machinery/miner/proc/set_miner_status()
	var/health_percent = round((miner_integrity / max_miner_integrity) * 100)
	switch(health_percent)
		if(-INFINITY to 0)
			miner_status = MINER_DESTROYED
			stored_mineral = 0
			camera.toggle_cam(null, FALSE)
		if(1 to 50)
			stored_mineral = 0
			miner_status = MINER_MEDIUM_DAMAGE
		if(51 to 99)
			stored_mineral = 0
			miner_status = MINER_SMALL_DAMAGE
		if(100 to INFINITY)
			start_processing()
			SSminimaps.remove_marker(src)
			var/marker_icon = "miner_[mineral_value >= PLATINUM_CRATE_SELL_AMOUNT ? "platinum" : "phoron"]_on"
			SSminimaps.add_marker(src, MINIMAP_FLAG_ALL, image('icons/UI_icons/map_blips.dmi', null, marker_icon, MINIMAP_BLIPS_LAYER))
			miner_status = MINER_RUNNING
			if(!camera.status)
				camera.toggle_cam(null, FALSE)
	update_icon()

/obj/machinery/miner/Destroy()
	qdel(camera)
	camera = null
	return ..()

/obj/machinery/miner/attack_ai(mob/user)
	return attack_hand(user)

///Called via global signal to prevent perpetual mining
/obj/machinery/miner/proc/disable_on_hijack()
	mineral_value = 0
	miner_integrity = 0
	set_miner_status()
