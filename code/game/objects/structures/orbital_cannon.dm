#define WARHEAD_FLY_TIME 1 SECONDS
#define RG_FLY_TIME 1 SECONDS
#define WARHEAD_FALLING_SOUND_RANGE 15

GLOBAL_DATUM(orbital_cannon, /obj/structure/orbital_cannon)
GLOBAL_DATUM(rail_gun, /obj/structure/ship_rail_gun)
GLOBAL_LIST_EMPTY(ob_type_fuel_requirements)

/obj/structure/orbital_cannon
	name = "\improper 轨道炮"
	desc = "TGMC轨道炮系统.用于射击所环绕行星上的大型目标.它用固体燃料加速其弹头,撞击时产生毁灭性效果."
	icon = 'icons/obj/machines/artillery.dmi'
	icon_state = "OBC_unloaded"
	density = TRUE
	anchored = TRUE
	layer = BELOW_OBJ_LAYER
	appearance_flags = PIXEL_SCALE|LONG_GLIDE
	bound_width = 128
	bound_height = 64
	bound_y = 64
	resistance_flags = RESIST_ALL
	allow_pass_flags = NONE
	var/obj/structure/orbital_tray/tray
	var/chambered_tray = FALSE
	var/loaded_tray = FALSE
	var/ob_cannon_busy = FALSE

/obj/structure/orbital_cannon/Initialize(mapload)
	. = ..()
	if(!GLOB.orbital_cannon)
		GLOB.orbital_cannon = src

	if(!length(GLOB.ob_type_fuel_requirements))
		var/list/L = list(3, 4, 5, 6)
		for(var/i in 1 to 4)
			GLOB.ob_type_fuel_requirements |= pick_n_take(L)

	var/turf/T = locate(x+1,y+1,z)
	var/obj/structure/orbital_tray/O = new(T)
	tray = O
	tray.linked_ob = src

/obj/structure/orbital_cannon/Destroy()
	if(tray)
		tray.linked_ob = null
		tray = null
	if(GLOB.orbital_cannon == src)
		GLOB.orbital_cannon = null
	QDEL_NULL(tray)
	return ..()

/obj/structure/orbital_cannon/update_icon_state()
	. = ..()
	if(chambered_tray)
		icon_state = "OBC_chambered"
		return
	if(loaded_tray)
		icon_state = "OBC_loaded"
	else
		icon_state = "OBC_unloaded"


/obj/structure/orbital_cannon/proc/load_tray(mob/user)
	set waitfor = 0

	if(!tray)
		return

	if(ob_cannon_busy)
		return

	if(!tray.warhead)
		if(user)
			to_chat(user, span_warning("托盘内没有弹头,装填操作已取消."))
		return

	if(tray.fuel_amt < 1)
		to_chat(user, span_warning("托盘内没有固体燃料,装填操作已取消."))
		return

	if(loaded_tray)
		to_chat(user, span_warning("托盘已经装填完毕."))
		return

	tray.forceMove(src)

	flick("OBC_loading",src)

	playsound(loc, 'sound/mecha/powerloader_buckle.ogg', 40)

	ob_cannon_busy = TRUE

	sleep(1 SECONDS)

	ob_cannon_busy = FALSE

	loaded_tray = TRUE

	update_icon()




/obj/structure/orbital_cannon/proc/unload_tray(mob/user)
	set waitfor = 0

	if(ob_cannon_busy)
		return

	if(chambered_tray)
		to_chat(user, span_warning("托盘在进入膛室后无法卸载,请先开火."))
		return

	if(!loaded_tray)
		to_chat(user, span_warning("托盘未装填."))
		return

	flick("OBC_unloading",src)

	playsound(loc, 'sound/mecha/powerloader_unbuckle.ogg', 40)

	ob_cannon_busy = TRUE

	sleep(1 SECONDS)

	ob_cannon_busy = FALSE

	var/turf/T = locate(x+1,y+1,z)

	tray.forceMove(T)
	loaded_tray = FALSE

	update_icon()





/obj/structure/orbital_cannon/proc/chamber_payload(mob/user)
	set waitfor = 0

	if(!loaded_tray)
		to_chat(user, span_warning("你需要先装填托盘才能将其送入膛室."))
		return

	if(ob_cannon_busy)
		return

	if(chambered_tray)
		return
	if(!tray)
		return
	if(!tray.warhead)
		if(user)
			to_chat(user, span_warning("托盘内没有弹头,取消送入膛室操作."))
		return

	if(tray.fuel_amt < 1)
		if(user)
			to_chat(user, span_warning("托盘内没有固体燃料,取消送入膛室操作."))
		return

	flick("OBC_chambering",src)

	playsound(loc, 'sound/machines/hydraulics_2.ogg', 40, 1)

	ob_cannon_busy = TRUE

	sleep(0.6 SECONDS)

	ob_cannon_busy = FALSE

	chambered_tray = TRUE

	update_icon()

/// Handles the playing of the Orbital Bombardment incoming sound and other visual and auditory effects of the cannon, usually a spiraling whistle noise but can be overridden.
/obj/structure/orbital_cannon/proc/handle_ob_firing_effects(turf/target, ob_sound = 'sound/effects/OB_incoming.ogg')
	flick("OBC_firing",src)
	for(var/mob/living/current_mob AS in GLOB.mob_living_list)
		if(current_mob.z == z)
			if(get_dist(src, current_mob) > 20)
				current_mob.playsound_local(current_mob, 'sound/effects/obalarm.ogg', 25)
			shake_camera(current_mob, 0.7 SECONDS)
			to_chat(current_mob, span_warning("[SSmapping.configs[SHIP_MAP].map_name]的甲板随着其轨道炮开火而震颤."))
			continue
		playsound(loc, 'sound/effects/obfire.ogg', 100, FALSE, 20, 4)
		if(current_mob.z != target.z)
			continue
		if(get_dist(current_mob, target) > WARHEAD_FALLING_SOUND_RANGE)
			continue
		current_mob.playsound_local(target, ob_sound, falloff = 2)
	new /obj/effect/temp_visual/ob_impact(target, tray.warhead)

/obj/structure/orbital_cannon/proc/fire_ob_cannon(turf/T, mob/user)
	set waitfor = FALSE

	if(ob_cannon_busy)
		return

	if(!chambered_tray || !loaded_tray || !tray || !tray.warhead)
		return

	ob_cannon_busy = TRUE

	var/inaccurate_fuel = 0

	switch(tray.warhead.warhead_kind)
		if("explosive")
			inaccurate_fuel = abs(GLOB.ob_type_fuel_requirements[1] - tray.fuel_amt)
		if("incendiary")
			inaccurate_fuel = abs(GLOB.ob_type_fuel_requirements[2] - tray.fuel_amt)
		if("cluster")
			inaccurate_fuel = abs(GLOB.ob_type_fuel_requirements[3] - tray.fuel_amt)
		if("plasma")
			inaccurate_fuel = abs(GLOB.ob_type_fuel_requirements[4] - tray.fuel_amt)

	var/turf/target = locate(T.x + inaccurate_fuel * pick(-2, 2),T.y + inaccurate_fuel * pick(-2, 2),T.z)

	// Give marines a warning if misfuelled.
	var/fuel_warning = "Уровень топлива боеголовки: корректный."
	if(inaccurate_fuel > 0)
		fuel_warning = "Уровень топлива боеголовки: некорректный.<br>Возможно смещение области поражения."

	GLOB.round_statistics.obs_fired++
	SSblackbox.record_feedback("tally", "round_statistics", 1, "obs_fired")
	priority_announce(
		message = "立即离开打击区域!<br><br>弹头类型: [tray.warhead.warhead_kind_rus].<br>[fuel_warning]<br>目标: [get_area(T)].",
		title = "检测到轨道轰炸发射指令!",
		type = ANNOUNCEMENT_PRIORITY,
		sound = 'sound/effects/OB_warning_announce.ogg',
		channel_override = SSsounds.random_available_channel(), // This way, we can't have it be cut off by other sounds.
		color_override = "red"
	)
	var/list/receivers = (GLOB.alive_human_list + GLOB.ai_list + GLOB.observer_list)
	for(var/mob/living/screentext_receiver AS in receivers)
		screentext_receiver.play_screen_text(HUD_ANNOUNCEMENT_FORMATTING("ОРБИТАЛЬНЫЙ УДАР", "ТИП СНАРЯДА: [uppertext(tray.warhead.warhead_kind_rus)]", LEFT_ALIGN_TEXT), new /atom/movable/screen/text/screen_text/picture/potrait/custom_mugshot(null, null, user))
	playsound(target, 'sound/effects/OB_warning_announce_novoiceover.ogg', 125, FALSE, 30, 10) //VOX-less version for xenomorphs

	var/impact_time = 10 SECONDS + (WARHEAD_FLY_TIME * (GLOB.current_orbit/3))

	addtimer(CALLBACK(src, TYPE_PROC_REF(/obj/structure/orbital_cannon, handle_ob_firing_effects), target), impact_time - (0.5 SECONDS))
	var/impact_timerid = addtimer(CALLBACK(src, TYPE_PROC_REF(/obj/structure/orbital_cannon, impact_callback), target, inaccurate_fuel), impact_time, TIMER_STOPPABLE)

	var/canceltext = "Warhead: [tray.warhead.warhead_kind]. Impact at [ADMIN_VERBOSEJMP(target)] <a href='byond://?_src_=holder;[HrefToken(TRUE)];cancelob=[impact_timerid]'>\[CANCEL OB\]</a>"
	message_admins("[span_prefix("OB FIRED:")] <span class='message linkify'> [canceltext]</span>")
	log_game("OB fired by [user] at [AREACOORD(src)], OB type: [tray.warhead.warhead_kind], timerid to cancel: [impact_timerid]")
	notify_ghosts("<b>[user]</b> has just fired \the <b>[src]</b> !", source = T, action = NOTIFY_JUMP)

	tray.warhead.impact_message(target, impact_time)

/obj/structure/orbital_cannon/proc/impact_callback(target,inaccurate_fuel)
	tray.warhead.warhead_impact(target, inaccurate_fuel)

	ob_cannon_busy = FALSE
	chambered_tray = FALSE
	tray.fuel_amt = 0
	if(tray.warhead)
		QDEL_NULL(tray.warhead)
	tray.update_icon()

	update_icon()

/obj/structure/orbital_tray
	name = "装填托盘"
	desc = "轨道炮的装填托盘."
	icon = 'icons/obj/structures/mainship_props64.dmi'
	icon_state = "cannon_tray"
	density = TRUE
	anchored = TRUE
	climbable = TRUE
	appearance_flags = PIXEL_SCALE|LONG_GLIDE
	layer = BELOW_OBJ_LAYER + 0.01
	bound_width = 64
	bound_height = 32
	resistance_flags = RESIST_ALL
	pixel_y = -9
	pixel_x = -6
	var/obj/structure/ob_ammo/warhead/warhead
	var/obj/structure/orbital_cannon/linked_ob
	var/fuel_amt = 0


/obj/structure/orbital_tray/Destroy()
	QDEL_NULL(warhead)
	if(linked_ob)
		linked_ob.tray = null
		linked_ob = null
	return ..()


/obj/structure/orbital_tray/update_overlays()
	. = ..()
	if(warhead)
		. += image("cannon_tray_[warhead.warhead_kind]")
	if(fuel_amt)
		. += image("cannon_tray_[fuel_amt]")

//Not calling parent because this isn't the typical pick up/put down
/obj/structure/orbital_tray/attack_powerloader(mob/living/user, obj/item/powerloader_clamp/attached_clamp)
	if(attached_clamp.loaded && istype(attached_clamp.loaded, /obj/structure/ob_ammo))
		var/obj/structure/ob_ammo/OA = attached_clamp.loaded

		if(OA.is_solid_fuel)
			if(fuel_amt >= 6)
				to_chat(user, span_warning("[src]无法接受更多固体燃料."))
				return

			if(!warhead)
				to_chat(user, span_warning("必须先在[src]中放置弹头."))
				return
			fuel_amt++
			qdel(OA)
		else
			if(warhead)
				to_chat(user, span_warning("[src]已经有弹头了."))
				return
			warhead = OA

		to_chat(user, span_notice("你将[OA]装入[src]."))
		playsound(src, 'sound/machines/hydraulics_1.ogg', 40, 1)

		if(!QDELETED(OA))
			OA.forceMove(src)

		attached_clamp.loaded = null
		attached_clamp.update_icon()
		update_icon()
		return

	if(fuel_amt)
		var/obj/structure/ob_ammo/ob_fuel/OF = new(attached_clamp.linked_powerloader)
		attached_clamp.loaded = OF
		fuel_amt--
	else if(warhead)
		warhead.forceMove(attached_clamp.linked_powerloader)
		attached_clamp.loaded = warhead
		warhead = null

	attached_clamp.update_icon()
	playsound(loc, 'sound/machines/hydraulics_2.ogg', 40, 1)
	to_chat(user, span_notice("你用[attached_clamp]抓住[attached_clamp.loaded]."))
	update_icon()


/obj/structure/ob_ammo
	name = "理论轨道炮弹药"
	density = TRUE
	anchored = TRUE
	climbable = TRUE
	icon = 'icons/obj/structures/mainship_props.dmi'
	resistance_flags = XENO_DAMAGEABLE
	interaction_flags = INTERACT_OBJ_DEFAULT|INTERACT_POWERLOADER_PICKUP_ALLOWED_BYPASS_ANCHOR
	coverage = 100
	var/is_solid_fuel = 0

/obj/structure/ob_ammo/examine(mob/user)
	. = ..()
	. += "Moving this will require some sort of lifter."

/obj/structure/ob_ammo/obj_destruction(damage_amount, damage_type, damage_flag, mob/living/blame_mob)
	cell_explosion(loc, 60, 30)
	flame_radius(2, loc)
	return ..()

/obj/structure/ob_ammo/warhead
	name = "理论轨道炮弹药"
	var/warhead_kind
	var/warhead_kind_rus

///Explode the warhead
/obj/structure/ob_ammo/warhead/proc/warhead_impact()
	return

/obj/structure/ob_ammo/warhead/proc/impact_message(turf/target, impact_time = 10 SECONDS)
	var/relative_dir
	for(var/mob/living/our_mob in range(30, target))
		if(get_turf(our_mob) == target)
			relative_dir = 0
		else
			relative_dir = get_dir(our_mob, target)
		our_mob.show_message(span_userdanger("天空闪起了<u>[relative_dir ? ("с " + dir2rutext(relative_dir) + "а от тебя") : "прямо над тобой"]</u>!"), EMOTE_VISIBLE,
			span_userdanger("从天空中传来巨大的声响<u>[relative_dir ? ("примерно с " + dir2rutext(relative_dir) + "а от тебя") : "прямо над тобой"]</u>!"), EMOTE_AUDIBLE)

	sleep(impact_time / 3)
	for(var/mob/living/our_mob in range(25, target))
		if(get_turf(our_mob) == target)
			relative_dir = 0
		else
			relative_dir = get_dir(our_mob, target)
		our_mob.show_message(span_userdanger("天空越来越响地咆哮着<u>[relative_dir ? ("с " + dir2rutext(relative_dir) + "а от тебя") : "прямо над тобой"]</u>!"), EMOTE_VISIBLE,
			span_userdanger("噪音变得更响了<u>[relative_dir ? ("где-то с " + dir2rutext(relative_dir) + "а от тебя") : "прямо над тобой"]</u>!"), EMOTE_AUDIBLE)

	sleep(impact_time / 3)
	for(var/mob/living/our_mob in range(15, target))
		our_mob.show_message(span_userdanger("天哪,天空现在要炸开了!!!"), EMOTE_VISIBLE,
			span_userdanger("快从这里滚开!"), EMOTE_AUDIBLE)

/obj/structure/ob_ammo/warhead/explosive
	name = "\improper 高爆轨道弹头"
	warhead_kind = "explosive"
	warhead_kind_rus = "взрывной"
	icon_state = "ob_warhead_1"
	var/explosion_power = 1425
	var/explosion_falloff = 90

/obj/structure/ob_ammo/warhead/explosive/warhead_impact(turf/target, inaccuracy_amt = 0)
	cell_explosion(target, explosion_power, explosion_falloff + (inaccuracy_amt * 10)) // inaccuracy adds up fallof in result range decreases

/obj/structure/ob_ammo/warhead/incendiary
	name = "\improper 燃烧轨道弹头"
	warhead_kind = "incendiary"
	warhead_kind_rus = "зажигательный"
	icon_state = "ob_warhead_2"
	var/flame_range_num
	var/flame_intensity = 46
	var/flame_duration = 40
	var/flame_colour = "blue"
	var/smoke_radius = 17
	var/smoke_duration = 20

/obj/structure/ob_ammo/warhead/incendiary/warhead_impact(turf/target, inaccuracy_amt = 0)
	flame_radius(15 - inaccuracy_amt, target, flame_intensity, flame_duration, colour = flame_colour)
	var/datum/effect_system/smoke_spread/phosphorus/warcrime = new
	warcrime.set_up(smoke_radius, target, smoke_duration)
	warcrime.start()

/obj/structure/ob_ammo/warhead/cluster
	name = "\improper 集束轨道弹头"
	warhead_kind = "cluster"
	warhead_kind_rus = "кластер"
	icon_state = "ob_warhead_3"
	var/cluster_amount = 25
	var/cluster_power = 240
	var/cluster_falloff = 40
	var/cluster_range

/obj/structure/ob_ammo/warhead/cluster/warhead_impact(turf/target, inaccuracy_amt = 0)
	set waitfor = FALSE
	cluster_range = max(9 - inaccuracy_amt, 6)
	var/list/turf_list = list()
	for(var/turf/T AS in RANGE_TURFS(cluster_range, target))
		turf_list += T
	var/clusters_to_shoot = max(cluster_amount - inaccuracy_amt, cluster_amount - 5)
	for(var/i = 1 to clusters_to_shoot)
		var/turf/U = pick_n_take(turf_list)
		cell_explosion(U, cluster_power, cluster_falloff, adminlog = FALSE) //rocket barrage
		sleep(0.1 SECONDS)

/obj/structure/ob_ammo/warhead/plasmaloss
	name = "\improper 等离子汲取轨道弹头"
	warhead_kind = "plasma"
	warhead_kind_rus = "Т-ГАЗ"
	icon_state = "ob_warhead_4"
	var/smoke_radius = 25
	var/smoke_duration = 3 SECONDS

/obj/structure/ob_ammo/warhead/plasmaloss/warhead_impact(turf/target, inaccuracy_amt = 0)
	var/datum/effect_system/smoke_spread/plasmaloss/smoke = new
	smoke.set_up(smoke_radius - inaccuracy_amt, target, smoke_duration - (inaccuracy_amt * 2))//Vape nation
	smoke.start()

/obj/structure/ob_ammo/ob_fuel
	name = "固体燃料"
	icon_state = "ob_fuel"
	is_solid_fuel = TRUE

/obj/structure/ob_ammo/ob_fuel/Initialize(mapload)
	. = ..()
	pixel_x = rand(-5, 5)
	pixel_y = rand(-5, 5)

/obj/machinery/computer/orbital_cannon_console
	name = "\improper Orbital Cannon Console"
	desc = "控制轨道炮装填系统的控制台."
	icon_state = "ob_console"
	screen_overlay = "ob_console_screen"
	dir = WEST
	layer = LOW_ITEM_LAYER
	atom_flags = ON_BORDER|CONDUCT
	interaction_flags = INTERACT_MACHINE_TGUI

/obj/machinery/computer/orbital_cannon_console/Initialize(mapload)
	. = ..()

	var/static/list/connections = list(
		COMSIG_ATOM_EXIT = PROC_REF(on_try_exit)
	)
	AddElement(/datum/element/connect_loc, connections)

/obj/machinery/computer/orbital_cannon_console/ex_act()
	return

/obj/machinery/computer/orbital_cannon_console/can_interact(mob/user)
	. = ..()
	if(!.)
		return FALSE

	if(!allowed(user))
		return FALSE

	if(!isobserver(user) && user.skills.getRating(SKILL_ENGINEER) < SKILL_ENGINEER_ENGI)
		user.visible_message(span_notice("[user]笨手笨脚地摸索着如何使用控制台."),
		span_notice("你笨手笨脚地摸索着如何使用控制台."))
		var/fumbling_time = 5 SECONDS * ( SKILL_ENGINEER_ENGI - user.skills.getRating(SKILL_ENGINEER) )
		if(!do_after(user, fumbling_time, NONE, src, BUSY_ICON_UNSKILLED))
			return FALSE

	return TRUE

/obj/machinery/computer/orbital_cannon_console/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "OrbitalCannonConsole", name)
		ui.open()

/obj/machinery/computer/orbital_cannon_console/ui_data(mob/user)
	. = list()
	.["cannon_linked"] = !!GLOB.orbital_cannon
	.["tray_linked"] = !!(GLOB.orbital_cannon?.tray)
	.["busy"] = !!(GLOB.orbital_cannon?.ob_cannon_busy)

	if(!GLOB.orbital_cannon || !GLOB.orbital_cannon.tray)
		return

	var/obj/structure/orbital_tray/tray = GLOB.orbital_cannon.tray

	.["loaded_tray"] = !!GLOB.orbital_cannon.loaded_tray
	.["chambered_tray"] = !!GLOB.orbital_cannon.chambered_tray
	.["warhead"] = tray.warhead?.name
	.["fuel"] = tray.fuel_amt

	var/list/fuel_requirements = list()
	if(length(GLOB.ob_type_fuel_requirements) >= 4)
		fuel_requirements += list(list("name" = "HE Orbital Warhead", "amount" = GLOB.ob_type_fuel_requirements[1]))
		fuel_requirements += list(list("name" = "Incendiary Orbital Warhead", "amount" = GLOB.ob_type_fuel_requirements[2]))
		fuel_requirements += list(list("name" = "Cluster Orbital Warhead", "amount" = GLOB.ob_type_fuel_requirements[3]))
		fuel_requirements += list(list("name" = "Plasma Drain Orbital Warhead", "amount" = GLOB.ob_type_fuel_requirements[4]))
	.["fuel_requirements"] = fuel_requirements

/obj/machinery/computer/orbital_cannon_console/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	if(.)
		return

	switch(action)
		if("load_tray")
			GLOB.orbital_cannon?.load_tray(usr)
			. = TRUE

		if("unload_tray")
			GLOB.orbital_cannon?.unload_tray(usr)
			. = TRUE

		if("chamber_tray")
			GLOB.orbital_cannon?.chamber_payload(usr)
			. = TRUE


/obj/structure/ship_rail_gun
	name = "\improper 电磁炮"
	desc = "一种强大的舰对舰武器,有时也用于地面支援,但效率会降低."
	icon = 'icons/obj/machines/artillery.dmi'
	icon_state = "Railgun"
	density = TRUE
	anchored = TRUE
	appearance_flags = PIXEL_SCALE|LONG_GLIDE
	layer = BELOW_OBJ_LAYER
	bound_width = 128
	bound_height = 64
	bound_y = 64
	resistance_flags = RESIST_ALL
	var/cannon_busy = FALSE
	var/last_firing = 0 //stores the last time it was fired to check when we can fire again
	var/last_firing_ai = 0 //same thing as last_firing but only cares when the AI last fired
	var/obj/structure/ship_ammo/railgun/rail_gun_ammo

/obj/structure/ship_rail_gun/Initialize(mapload)
	. = ..()
	if(!GLOB.rail_gun)
		GLOB.rail_gun = src
	rail_gun_ammo = new /obj/structure/ship_ammo/railgun(src)
	rail_gun_ammo.max_ammo_count = 8000 //200 uses or 15 full minutes of firing.
	rail_gun_ammo.ammo_count = 8000

/obj/structure/ship_rail_gun/Destroy()
	if(GLOB.rail_gun == src)
		GLOB.rail_gun = null
	QDEL_NULL(rail_gun_ammo)
	return ..()

/obj/structure/ship_rail_gun/proc/fire_rail_gun(turf/T, mob/user, ignore_cooldown = FALSE, ai_operation = FALSE)
	if(cannon_busy && !ignore_cooldown)
		return
	if(!rail_gun_ammo?.ammo_count)
		to_chat(user, span_warning("[src]的弹药已经耗尽."))
		return
	flick("Railgun_firing",src)
	cannon_busy = TRUE
	if(ai_operation)
		last_firing_ai = world.time
	else
		last_firing = world.time
	playsound(loc, 'sound/weapons/guns/fire/tank_smokelauncher.ogg', 70, 1)
	playsound(loc, 'sound/weapons/guns/fire/pred_plasma_shot.ogg', 70, 1)
	var/turf/target = locate(T.x + rand(-4, 4), T.y + rand(-4, 4), T.z)
	for(var/mob/living/silicon/ai/AI AS in GLOB.ai_list)
		to_chat(AI, span_notice("通知 - \The [src]已开火."))
	rail_gun_ammo.ammo_count = max(0, rail_gun_ammo.ammo_count - rail_gun_ammo.ammo_used_per_firing)
	addtimer(CALLBACK(src, TYPE_PROC_REF(/obj/structure/ship_rail_gun, impact_rail_gun), target), 1 SECONDS + (RG_FLY_TIME * (GLOB.current_orbit/3)))

/obj/structure/ship_rail_gun/proc/impact_rail_gun(turf/T)
	rail_gun_ammo.detonate_on(T)
	cannon_busy = FALSE
