/datum/storage/internal
	allow_drawing_method = FALSE /// Unable to set draw_mode ourselves

//Reason for this override is due to conflict signal from modules, which detach on ALT+CLICK
/datum/storage/internal/register_storage_signals(atom/parent)
	//Clicking signals
	RegisterSignal(parent, COMSIG_ATOM_ATTACKBY, PROC_REF(on_attackby)) //Left click
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_HAND, PROC_REF(on_attack_hand)) //Left click empty hand
	RegisterSignal(parent, COMSIG_ITEM_ATTACK_SELF, PROC_REF(on_attack_self)) //Item clicking on itself
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_HAND_ALTERNATE, PROC_REF(on_attack_hand_alternate)) //Right click empty hand
	RegisterSignal(parent, COMSIG_CLICK_ALT_RIGHT, PROC_REF(on_alt_right_click)) //ALT + right click
	RegisterSignal(parent, COMSIG_CLICK_CTRL, PROC_REF(on_ctrl_click)) //CTRL + Left click
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_GHOST, PROC_REF(on_attack_ghost)) //Ghosts can see inside your storages
	RegisterSignal(parent, COMSIG_MOUSEDROP_ONTO, PROC_REF(on_mousedrop_onto)) //Click dragging

	//Something is happening to our storage
	RegisterSignal(parent, COMSIG_CONTENTS_EX_ACT, PROC_REF(on_contents_explode)) //Getting exploded

	RegisterSignal(parent, COMSIG_ATOM_CONTENTS_DEL, PROC_REF(handle_atom_del))
	RegisterSignal(parent, ATOM_MAX_STACK_MERGING, PROC_REF(max_stack_merging))
	RegisterSignal(parent, ATOM_RECALCULATE_STORAGE_SPACE, PROC_REF(recalculate_storage_space))
	RegisterSignals(parent, list(COMSIG_ITEM_EQUIPPED, COMSIG_ITEM_DROPPED), PROC_REF(update_verbs))
	RegisterSignal(parent, COMSIG_ITEM_QUICK_EQUIP, PROC_REF(on_quick_equip_request))
	RegisterSignal(parent, COMSIG_ATOM_INITIALIZED_ON, PROC_REF(item_init_in_parent))

//Reason for this override is due to conflict signal from modules, which detach on ALT+CLICK
/datum/storage/internal/unregister_storage_signals(atom/parent)
	UnregisterSignal(parent, list(
		COMSIG_ATOM_ATTACKBY,
		COMSIG_ATOM_ATTACK_HAND,
		COMSIG_ITEM_ATTACK_SELF,
		COMSIG_ATOM_ATTACK_HAND_ALTERNATE,
		COMSIG_CLICK_ALT_RIGHT,
		COMSIG_CLICK_CTRL,
		COMSIG_ATOM_ATTACK_GHOST,
		COMSIG_MOUSEDROP_ONTO,

		COMSIG_CONTENTS_EX_ACT,

		COMSIG_ATOM_CONTENTS_DEL,
		ATOM_MAX_STACK_MERGING,
		ATOM_RECALCULATE_STORAGE_SPACE,
		COMSIG_ITEM_EQUIPPED,
		COMSIG_ITEM_DROPPED,
		COMSIG_ITEM_QUICK_EQUIP,
		COMSIG_ATOM_INITIALIZED_ON,
	))

/datum/storage/internal/handle_item_insertion(obj/item/W, prevent_warning = FALSE)
	. = ..()
	var/obj/master_item = parent.loc
	master_item?.update_icon()

/datum/storage/internal/remove_from_storage(obj/item/item, atom/new_location, mob/user, silent = FALSE, bypass_delay = FALSE)
	. = ..()
	var/obj/master_item = parent.loc
	if(isturf(master_item) || ismob(master_item))
		return
	master_item?.update_icon()

/datum/storage/internal/on_attackby(datum/source, obj/item/attacking_item, mob/user, params)
	if(!ismodulararmormodule(attacking_item))
		return ..()

/datum/storage/internal/motorbike_pack
	storage_slots = 4
	max_w_class = WEIGHT_CLASS_BULKY
	max_storage_space = 8

/datum/storage/internal/motorbike_pack/on_ctrl_click()
	return //We want to be able to grab the bike without pulling something out

/datum/storage/internal/motorbike_pack/on_attackby(datum/source, obj/item/attacking_item, mob/user, params)
	if(!params) //we're clicking directly on storage, not the sprite. Avoids accidental storing
		return ..()

/datum/storage/internal/webbing
	max_w_class = WEIGHT_CLASS_SMALL
	storage_slots = 3

/datum/storage/internal/webbing/New(atom/parent)
	. = ..()
	set_holdable(
		cant_hold_list = list(
			/obj/item/stack/razorwire,
			/obj/item/stack/sheet,
			/obj/item/stack/sandbags,
			/obj/item/stack/snow,
			/obj/item/cell/lasgun/volkite/powerpack/marine_back,
		),
		storage_type_limits_list = list(
			/obj/item/ammo_magazine/rifle,
			/obj/item/ammo_magazine/smg,
			/obj/item/ammo_magazine/sniper,
			/obj/item/cell/lasgun,
			/obj/item/tool/surgery/solderingtool,
		)
	)

/datum/storage/internal/vest
	storage_slots = 5
	max_w_class = WEIGHT_CLASS_SMALL

/datum/storage/internal/vest/New(atom/parent)
	. = ..()
	set_holdable(cant_hold_list = list(
		/obj/item/stack/razorwire,
		/obj/item/stack/sheet,
		/obj/item/stack/sandbags,
		/obj/item/stack/snow,
		/obj/item/cell/lasgun/volkite/powerpack,
		/obj/item/cell/lasgun/plasma,
	))

/datum/storage/internal/white_vest
	max_w_class = WEIGHT_CLASS_BULKY
	storage_slots = 6 //one more than the brown webbing but you lose out on being able to hold non-medic stuff
	max_storage_space = 24

/datum/storage/internal/white_vest/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/healthanalyzer,
		/obj/item/stack/medical,
		/obj/item/reagent_containers/hypospray,
		/obj/item/reagent_containers/hypospray/advanced,
		/obj/item/reagent_containers/hypospray/autoinjector,
		/obj/item/reagent_containers/glass/bottle,
		/obj/item/reagent_containers/syringe,
		/obj/item/reagent_containers/pill,
		/obj/item/storage/pill_bottle,
		/obj/item/clothing/glasses/hud/health,
		/obj/item/clothing/gloves/latex,
		/obj/item/tweezers,
		/obj/item/tweezers_advanced,
		/obj/item/bodybag,
		/obj/item/roller,
		/obj/item/whistle,
		/obj/item/tool/surgery/solderingtool,
	))

/datum/storage/internal/surgery_webbing
	storage_slots = 13
	max_storage_space = 26

/datum/storage/internal/surgery_webbing/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/tool/surgery,
		/obj/item/stack/nanopaste,
		/obj/item/tweezers,
		/obj/item/tweezers_advanced,
	))

/datum/storage/internal/holster
	storage_slots = 4
	max_storage_space = 10
	max_w_class = WEIGHT_CLASS_BULKY

/datum/storage/internal/holster/New(atom/parent)
	. = ..()
	set_holdable(
		can_hold_list = list(
			/obj/item/weapon/gun/pistol,
			/obj/item/ammo_magazine/pistol,
			/obj/item/weapon/gun/revolver,
			/obj/item/ammo_magazine/revolver,
			/obj/item/weapon/gun/energy/lasgun/lasrifle/standard_marine_pistol,
			/obj/item/cell/lasgun/lasrifle,
		),
		cant_hold_list = list(
			/obj/item/weapon/gun/revolver/coltrifle,
		)
	)
	storage_type_limits_max = list(/obj/item/weapon/gun = 1)

/datum/storage/internal/modular
	max_storage_space = 2
	storage_slots = 2
	max_w_class = WEIGHT_CLASS_TINY

/datum/storage/internal/modular/New(atom/parent)
	. = ..()
	set_holdable(
		can_hold_list = list(/obj/item/stack),
		storage_type_limits_list = list(/obj/item/clothing/glasses)
	)

/datum/storage/internal/pocket
	max_storage_space = 6
	storage_slots = 2
	max_w_class = WEIGHT_CLASS_NORMAL

/datum/storage/internal/pocket/New(atom/parent)
	. = ..()
	set_holdable(
		cant_hold_list = list(
			/obj/item/cell/lasgun/volkite/powerpack/marine_back,
		),
		storage_type_limits_list = list(
			/obj/item/ammo_magazine/rifle,
			/obj/item/cell/lasgun,
			/obj/item/ammo_magazine/smg,
			/obj/item/ammo_magazine/pistol,
			/obj/item/ammo_magazine/revolver,
			/obj/item/ammo_magazine/sniper,
			/obj/item/ammo_magazine/handful,
		),
	)

/datum/storage/internal/pocket/insertion_message(obj/item/item, mob/user)
	var/vision_distance = item.w_class >= WEIGHT_CLASS_NORMAL ? 3 : 1
	//Grab the name of the object this pocket belongs to
	user.visible_message(span_notice("[user]将\a [item]放入\the [parent.name]."), span_notice("你将\the [item]放入\the [parent.name]."), null, vision_distance)

/datum/storage/internal/pocket/medical
	max_storage_space = 30
	storage_slots = 5
	max_w_class = WEIGHT_CLASS_SMALL

/datum/storage/internal/pocket/medical/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/healthanalyzer,
		/obj/item/stack/medical,
		/obj/item/reagent_containers/hypospray,
		/obj/item/reagent_containers/hypospray/advanced,
		/obj/item/reagent_containers/hypospray/autoinjector,
		/obj/item/reagent_containers/glass/bottle,
		/obj/item/reagent_containers/syringe,
		/obj/item/reagent_containers/pill,
		/obj/item/storage/pill_bottle,
		/obj/item/clothing/glasses/hud/health,
		/obj/item/clothing/gloves/latex,
		/obj/item/tweezers,
		/obj/item/tweezers_advanced,
		/obj/item/whistle,
	))

/datum/storage/internal/general
	max_storage_space = 6
	storage_slots = 2
	max_w_class = WEIGHT_CLASS_NORMAL

/datum/storage/internal/general/New(atom/parent)
	. = ..()
	set_holdable(
		cant_hold_list = list(/obj/item/cell/lasgun/volkite/powerpack/marine_back),
		storage_type_limits_list = list(
			/obj/item/ammo_magazine/rifle,
			/obj/item/cell/lasgun,
			/obj/item/ammo_magazine/smg,
			/obj/item/ammo_magazine/pistol,
			/obj/item/ammo_magazine/revolver,
			/obj/item/ammo_magazine/sniper,
			/obj/item/ammo_magazine/handful,
			/obj/item/cell/lasgun/plasma,
		)
	)

/datum/storage/internal/ammo_mag
	max_storage_space = 15
	storage_slots = 4
	max_w_class = WEIGHT_CLASS_NORMAL

/datum/storage/internal/ammo_mag/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/weapon/combat_knife,
		/obj/item/attachable/bayonetknife,
		/obj/item/explosive/grenade/flare/civilian,
		/obj/item/explosive/grenade/flare,
		/obj/item/ammo_magazine/rifle,
		/obj/item/cell/lasgun,
		/obj/item/ammo_magazine/smg,
		/obj/item/ammo_magazine/pistol,
		/obj/item/ammo_magazine/revolver,
		/obj/item/ammo_magazine/sniper,
		/obj/item/ammo_magazine/handful,
		/obj/item/explosive/grenade,
		/obj/item/explosive/mine,
		/obj/item/reagent_containers/food/snacks,
	))

/datum/storage/internal/engineering
	max_storage_space = 15
	storage_slots = 5
	max_w_class = WEIGHT_CLASS_BULKY

/datum/storage/internal/engineering/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/stack/barbed_wire,
		/obj/item/stack/sheet,
		/obj/item/stack/rods,
		/obj/item/stack/cable_coil,
		/obj/item/stack/sandbags_empty,
		/obj/item/stack/sandbags,
		/obj/item/stack/razorwire,
		/obj/item/tool/shovel/etool,
		/obj/item/tool/wrench,
		/obj/item/tool/weldingtool,
		/obj/item/tool/wirecutters,
		/obj/item/tool/crowbar,
		/obj/item/tool/screwdriver,
		/obj/item/tool/handheld_charger,
		/obj/item/tool/multitool,
		/obj/item/binoculars/tactical/range,
		/obj/item/explosive/plastique,
		/obj/item/explosive/grenade/chem_grenade/razorburn_small,
		/obj/item/explosive/grenade/chem_grenade/razorburn_large,
		/obj/item/cell/apc,
		/obj/item/cell/high,
		/obj/item/cell/rtg,
		/obj/item/cell/super,
		/obj/item/cell/potato,
		/obj/item/assembly/signaler,
		/obj/item/explosive/plastique/detpack,
		/obj/item/circuitboard,
		/obj/item/lightreplacer,
		/obj/item/tool/surgery/solderingtool,
		/obj/item/minerupgrade,
	))

/datum/storage/internal/medical
	max_storage_space = 30
	storage_slots = 5
	max_w_class = WEIGHT_CLASS_SMALL

/datum/storage/internal/medical/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/healthanalyzer,
		/obj/item/stack/medical,
		/obj/item/reagent_containers/hypospray,
		/obj/item/reagent_containers/hypospray/advanced,
		/obj/item/reagent_containers/hypospray/autoinjector,
		/obj/item/reagent_containers/glass/bottle,
		/obj/item/reagent_containers/syringe,
		/obj/item/reagent_containers/pill,
		/obj/item/storage/syringe_case,
		/obj/item/roller/medevac,
		/obj/item/roller,
		/obj/item/bodybag,
		/obj/item/storage/pill_bottle,
		/obj/item/clothing/glasses/hud/health,
		/obj/item/clothing/gloves/latex,
		/obj/item/tweezers,
		/obj/item/tweezers_advanced,
		/obj/item/whistle,
		/obj/item/tool/surgery/solderingtool,
	))

/datum/storage/internal/injector
	max_storage_space = 14
	storage_slots = 14
	max_w_class = WEIGHT_CLASS_TINY

/datum/storage/internal/injector/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/reagent_containers/syringe,
		/obj/item/reagent_containers/hypospray/autoinjector,
	))

/datum/storage/internal/integrated
	storage_slots = null
	max_storage_space = 15
	max_w_class = WEIGHT_CLASS_NORMAL
	access_delay = 1.5 SECONDS

/datum/storage/internal/integrated/should_access_delay(obj/item/item, mob/user, taking_out) //defaults to 0
	if(!taking_out) // Always allow items to be tossed in instantly
		return FALSE
	return TRUE

/datum/storage/internal/grenade
	max_storage_space = 12
	storage_slots = 6
	max_w_class = WEIGHT_CLASS_SMALL

/datum/storage/internal/grenade/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(
		/obj/item/explosive/grenade,
		/obj/item/reagent_containers/food/drinks/cans,
	))

/datum/storage/internal/shoes
	max_storage_space = 4
	storage_slots = 2
	draw_mode = TRUE

/datum/storage/internal/shoes/New(atom/parent)
	. = ..()
	set_holdable(
		cant_hold_list = list(
			/obj/item/stack/sheet,
		),
		storage_type_limits_list = list(
			/obj/item/weapon/combat_knife,
			/obj/item/weapon/gun/pistol/p17,
			/obj/item/weapon/gun/shotgun/double/derringer,
			/obj/item/attachable/bayonetknife,
			/obj/item/attachable/bayonetknife/som,
			/obj/item/weapon/yautja/knife,
			/obj/item/stack/throwing_knife,
			/obj/item/storage/box/mre,
		)
	)

/datum/storage/internal/marinehelmet
	max_storage_space = 3
	storage_slots = 2
	max_w_class = WEIGHT_CLASS_TINY
	var/list/helmet_cosmetics = list(null, null)
	var/list/helmet_cosmetic_slots

/datum/storage/internal/marinehelmet/New(atom/parent)
	. = ..()
	helmet_cosmetic_slots = list(
		new /atom/movable/screen/helmet_cosmetic,
		new /atom/movable/screen/helmet_cosmetic,
	)
	for(var/i in 1 to length(helmet_cosmetic_slots))
		var/atom/movable/screen/helmet_cosmetic/slot = helmet_cosmetic_slots[i]
		slot.master = src
		slot.cosmetic_index = i
		slot.screen_loc = "[5 + i]:16,2:16"
	set_holdable(
		cant_hold_list = list(
			/obj/item/stack/sheet,
			/obj/item/stack/catwalk,
			/obj/item/stack/rods,
			/obj/item/stack/sandbags_empty,
			/obj/item/stack/tile,
			/obj/item/stack/cable_coil,
		),
		storage_type_limits_list = list(
			/obj/item/clothing/glasses,
			/obj/item/reagent_containers/food/snacks,
			/obj/item/stack/medical/heal_pack/gauze,
			/obj/item/stack/medical/heal_pack/ointment,
			/obj/item/ammo_magazine/handful,
		)
	)

/datum/storage/internal/marinehelmet/Destroy(force = FALSE, ...)
	QDEL_LIST(helmet_cosmetic_slots)
	helmet_cosmetics = null
	return ..()

/datum/storage/internal/marinehelmet/show_to(mob/user)
	. = ..()
	if(!user?.client)
		return
	closer.screen_loc = "8:16,2:16"
	user.client.screen += helmet_cosmetic_slots
	for(var/i in 1 to length(helmet_cosmetic_slots))
		var/atom/movable/screen/helmet_cosmetic/slot = helmet_cosmetic_slots[i]
		slot.update_cosmetic(helmet_cosmetics[i])

/datum/storage/internal/marinehelmet/hide_from(mob/user)
	if(user?.client)
		user.client.screen -= helmet_cosmetic_slots
	return ..()

/datum/storage/internal/marinehelmet/proc/set_helmet_cosmetic(index, obj/item/item, mob/user)
	if(index < 1 || index > 2 || !istype(item))
		return FALSE
	if(HAS_TRAIT(item, TRAIT_NODROP))
		return FALSE
	if(item.item_flags & (ITEM_ABSTRACT|HAND_ITEM))
		return FALSE
	if(length(can_hold) && !is_type_in_typecache(item, can_hold))
		return FALSE
	if(is_type_in_typecache(item, cant_hold))
		return FALSE
	if(!is_type_in_typecache(item, storage_type_limits) && item.w_class > max_w_class)
		return FALSE

	item.update_icon()
	var/obj/item/cloned_item = new item.type()
	cloned_item.appearance = item.appearance
	helmet_cosmetics[index] = cloned_item
	var/obj/item/clothing/head/modular/helmet = parent.loc
	if(istype(helmet))
		helmet.update_icon()
	for(var/mob/M in can_see_content())
		show_to(M)
	return TRUE


/datum/storage/internal/marinehelmet/proc/remove_helmet_cosmetic(index)
	if(index < 1 || index > 2)
		return FALSE
	var/obj/item/item = helmet_cosmetics[index]
	if(!item)
		return FALSE

	helmet_cosmetics[index] = null
	qdel(item)
	var/obj/item/clothing/head/modular/helmet = parent.loc
	if(istype(helmet))
		helmet.update_icon()
	for(var/mob/M in can_see_content())
		show_to(M)
	return TRUE

/atom/movable/screen/helmet_cosmetic
	name = "cosmetic storage"
	icon = 'icons/mob/screen/generic.dmi'
	icon_state = "block"
	color = "#3b82f6"
	plane = ABOVE_HUD_PLANE
	mouse_over_pointer = MOUSE_HAND_POINTER
	var/cosmetic_index = 0

/atom/movable/screen/helmet_cosmetic/Click(location, control, params)
	if(!ishuman(usr) || usr.incapacitated(TRUE))
		return TRUE
	var/list/modifiers = params2list(params)
	if(modifiers["right-click"])
		return TRUE
	var/datum/storage/internal/marinehelmet/storage = master
	if(!istype(storage))
		return TRUE

	var/mob/living/carbon/human/user = usr
	var/obj/item/item_in_hand = user.get_active_held_item()
	if(item_in_hand)
		storage.set_helmet_cosmetic(cosmetic_index, item_in_hand, user)
	else
		storage.remove_helmet_cosmetic(cosmetic_index)
	return TRUE

/atom/movable/screen/helmet_cosmetic/proc/update_cosmetic(obj/item/cosmetic)
	overlays.Cut()
	if(!istype(cosmetic))
		return

	var/mutable_appearance/appearance = new(cosmetic.appearance)
	appearance.appearance_flags |= RESET_COLOR
	appearance.plane = FLOAT_PLANE
	overlays += appearance

/atom/movable/screen/helmet_cosmetic/MouseDrop_T(atom/movable/O, mob/user)
	. = ..()
	if(!ishuman(user) || user.incapacitated(TRUE))
		return TRUE
	if(!istype(O, /obj/item))
		return TRUE
	var/datum/storage/internal/marinehelmet/storage = master
	if(!istype(storage))
		return TRUE
	if(storage.helmet_cosmetics[cosmetic_index])
		return TRUE

	storage.set_helmet_cosmetic(cosmetic_index, O, user)
	return TRUE

/datum/storage/internal/ammo_rack //Hey isn't this great? Due to this storage refactor, deployables can have storage too!
	storage_slots = 10
	max_storage_space = 40
	max_w_class = WEIGHT_CLASS_BULKY

/datum/storage/internal/ammo_rack/New(atom/parent)
	. = ..()
	set_holdable(can_hold_list = list(/obj/item/ammo_magazine/at36))

//Reason for this override is due to conflict controls from deployables
/datum/storage/internal/ammo_rack/register_storage_signals(atom/parent)
	//Clicking signals
	RegisterSignal(parent, COMSIG_ATOM_ATTACKBY, PROC_REF(on_attackby)) //Left click
	RegisterSignal(parent, COMSIG_ITEM_ATTACK_SELF, PROC_REF(on_attack_self)) //Item clicking on itself
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_HAND_ALTERNATE, PROC_REF(on_attack_hand_alternate)) //Right click empty hand
	RegisterSignal(parent, COMSIG_ATOM_ATTACK_GHOST, PROC_REF(on_attack_ghost)) //Ghosts can see inside your storages

	//Something is happening to our storage
	RegisterSignal(parent, COMSIG_CONTENTS_EX_ACT, PROC_REF(on_contents_explode)) //Getting exploded

	RegisterSignal(parent, COMSIG_ATOM_CONTENTS_DEL, PROC_REF(handle_atom_del))
	RegisterSignal(parent, ATOM_MAX_STACK_MERGING, PROC_REF(max_stack_merging))
	RegisterSignal(parent, ATOM_RECALCULATE_STORAGE_SPACE, PROC_REF(recalculate_storage_space))
	RegisterSignals(parent, list(COMSIG_ITEM_EQUIPPED, COMSIG_ITEM_DROPPED), PROC_REF(update_verbs))
	RegisterSignal(parent, COMSIG_ITEM_QUICK_EQUIP, PROC_REF(on_quick_equip_request))
	RegisterSignal(parent, COMSIG_ATOM_INITIALIZED_ON, PROC_REF(item_init_in_parent))

//Reason for this override is due to conflict controls from deployables
/datum/storage/internal/ammo_rack/unregister_storage_signals(atom/parent)
	UnregisterSignal(parent, list(
		COMSIG_ATOM_ATTACKBY,
		COMSIG_ITEM_ATTACK_SELF,
		COMSIG_ATOM_ATTACK_HAND_ALTERNATE,
		COMSIG_ATOM_ATTACK_GHOST,

		COMSIG_CONTENTS_EX_ACT,

		COMSIG_ATOM_CONTENTS_DEL,
		ATOM_MAX_STACK_MERGING,
		ATOM_RECALCULATE_STORAGE_SPACE,
		COMSIG_ITEM_EQUIPPED,
		COMSIG_ITEM_DROPPED,
		COMSIG_ITEM_QUICK_EQUIP,
		COMSIG_ATOM_INITIALIZED_ON,
	))

// Special override to reload our gun if it's empty before putting extra shells into storage
/datum/storage/internal/ammo_rack/on_attackby(datum/source, obj/item/attacking_item, mob/user, params)
	if(user.active_storage != src) //Only insert shells into storage if our storage UI is open
		return FALSE

	if(length(refill_types))
		for(var/typepath in refill_types)
			if(istype(attacking_item, typepath))
				INVOKE_ASYNC(src, PROC_REF(do_refill), attacking_item, user)
				return

	if(!can_be_inserted(attacking_item, user))
		return FALSE
	INVOKE_ASYNC(src, PROC_REF(handle_item_insertion), attacking_item, FALSE, user)
	return COMPONENT_NO_AFTERATTACK

/datum/storage/internal/ammo_rack/on_attack_hand_alternate(datum/source, mob/living/user) //Override for subtype since this is in world storage
	if(user.CanReach(source))
		open(user)
