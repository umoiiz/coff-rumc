/datum/job/som // left for eord
	job_category = JOB_CAT_MARINE
	access = ALL_ANTAGONIST_ACCESS
	minimal_access = ALL_ANTAGONIST_ACCESS
	faction = FACTION_SOM

//Base job for normal gameplay SOM, not ERT.
/datum/job/som/squad
	access = ALL_ANTAGONIST_ACCESS
	minimal_access = ALL_ANTAGONIST_ACCESS
	supervisors = "the acting squad leader"
	selection_color = "#ffeeee"
	exp_type_department = EXP_TYPE_MARINES

/datum/job/som/squad/after_spawn(mob/living/carbon/human/human_spawn, mob/M, latejoin = FALSE)
	. = ..()
	if(!ishuman(human_spawn))
		return
	human_spawn.hud_set_job(faction)
	if(!(human_spawn.species.species_flags & ROBOTIC_LIMBS))
		human_spawn.set_nutrition(250)
	if(!human_spawn.assigned_squad)
		CRASH("after_spawn called for a marine without an assigned_squad")
	to_chat(M, {"\nYou已被分配到:<b><font size=3 color=[human_spawn.assigned_squad.color]>[lowertext(human_spawn.assigned_squad.name)]小队</font></b>.
前往食堂吃些低温休眠后的食物,然后到你的小队准备室领取装备."})

/datum/job/som/squad/equip_spawning_squad(mob/living/carbon/human/new_character, datum/squad/assigned_squad, client/player)
	if(!assigned_squad)
		SSjob.JobDebug("Failed to put marine role in squad. Player: [player.key] Job: [title]")
		return
	assigned_squad.insert_into_squad(new_character)

//SOM Standard
/datum/job/som/squad/standard
	paygrade = "SOM_E1"
	comm_title = "Mar"
	minimap_icon = "private"
	display_order = JOB_DISPLAY_ORDER_SQUAD_MARINE
	total_positions = -1
	//job_flags = JOB_FLAG_LATEJOINABLE|JOB_FLAG_ROUNDSTARTJOINABLE|JOB_FLAG_ALLOWS_PREFS_GEAR|JOB_FLAG_PROVIDES_BANK_ACCOUNT|JOB_FLAG_ADDTOMANIFEST|JOB_FLAG_PROVIDES_SQUAD_HUD|JOB_FLAG_CAN_SEE_ORDERS
	outfit = /datum/outfit/job/som/squad/standard
	jobworth = list(
		/datum/job/xenomorph = LARVA_POINTS_REGULAR,
		/datum/job/som/squad/veteran = VETERAN_POINTS_REGULAR,
	)
	html_description = {"
		<b>Difficulty</b>: Easy<br /><br />
		<b>You answer to the</b> acting Squad Leader<br /><br />
		<b>Unlock Requirement</b>: Starting Role<br /><br />
		<b>Gamemode Availability</b>: Combat patrol and Sensor Capture<br /><br /><br />
		The backbone of the Sons of Mars are their rank and file marines, trained and equipped to fight the conventional military of their former oppressors. They are fitted with the standard arsenal that the SOM offers, equipped with traditional projectile weaponry as well are less common but more deadly volkite weapons as the SOM's industry allows. They’re often high in numbers and divided into squads, but they’re the lowest ranking individuals, with a low degree of skill, not adapt to engineering or medical roles. Still, they are not limited to the arsenal they can take on the field to deal whatever threat that lurks against the Sons of Mars.
		<br /><br />
		<b>Duty</b>: Carry out orders made by your acting Squad Leader, deal with any threats that oppose the Sons of Mars.
	"}
	job_desc = "Выполняйте приказы исполняющего обязанности командира отряда и устраняйте любые угрозы для Сынов Марса."

/datum/job/som/squad/standard/after_spawn(mob/living/carbon/new_mob, mob/user, latejoin = FALSE)
	. = ..()
	if(!ishuman(new_mob))
		return
	var/mob/living/carbon/human/new_human = new_mob
	var/playtime_mins = user?.client?.get_exp(title)
	if(!playtime_mins || playtime_mins < 1 )
		return
	switch(playtime_mins)
		if(0 to 600) // starting
			new_human.wear_id.paygrade = "SOM_E1"
		if(601 to 6000) // 10hrs
			new_human.wear_id.paygrade = "SOM_E2"
		if(6001 to 18000) // 100 hrs
			new_human.wear_id.paygrade = "SOM_E3"
		if(18001 to 30000) // 300 hrs
			new_human.wear_id.paygrade = "SOM_E4"
		if(30001 to 60000) // 500 hrs
			new_human.wear_id.paygrade = "SOM_E5"
		if(60001 to INFINITY) // 1000 hrs
			new_human.wear_id.paygrade = "SOM_S1"

/datum/job/som/squad/standard/radio_help_message(mob/M)
	. = ..()
	to_chat(M, {"\nYou是火星之子的普通士兵,这就是你的力量.
你独自一人所缺乏的,能在与SOM的男女并肩作战中获得.为了火星!"})

/datum/job/som/squad/engineer
	paygrade = "SOM_E3"
	comm_title = "Eng"
	total_positions = 12
	skills_type = /datum/skills/combat_engineer
	display_order = JOB_DISPLAY_ORDER_SUQAD_ENGINEER
	outfit = /datum/outfit/job/som/squad/engineer
	//job_flags = JOB_FLAG_LATEJOINABLE|JOB_FLAG_ROUNDSTARTJOINABLE|JOB_FLAG_ALLOWS_PREFS_GEAR|JOB_FLAG_PROVIDES_BANK_ACCOUNT|JOB_FLAG_ADDTOMANIFEST|JOB_FLAG_PROVIDES_SQUAD_HUD|JOB_FLAG_CAN_SEE_ORDERS
	jobworth = list(
		/datum/job/xenomorph = LARVA_POINTS_REGULAR,
		/datum/job/som/squad/veteran = VETERAN_POINTS_REGULAR,
	)
	html_description = {"
		<b>Difficulty</b>: Medium<br /><br />
		<b>You answer to the</b> acting Squad Leader<br /><br />
		<b>Unlock Requirement</b>: Starting Role<br /><br />
		<b>Gamemode Availability</b>: Combat patrol and Sensor Capture<br /><br /><br />
		A mastermind of on-field construction, often regarded as the answer on whether the FOB succeeds or not, Squad Engineers are the people who construct the Forward Operating Base (FOB) and guard whatever threat that endangers the marines. In addition to this, they are also in charge of repairing power generators on the field as well as mining drills for requisitions. They have a high degree of engineering skill, meaning they can deploy and repair barricades faster than regular marines.
		<br /><br />
		<b>Duty</b>: Construct and reinforce the FOB that has been ordered by your acting Squad Leader, fix power generators and mining drills in the AO and stay on guard for any dangers that threaten your FOB.
	"}
	job_desc = "Стройте и укрепляйте по приказу командира отряда, чините генераторы и буровые установки в районе операции и следите за угрозами для вашей базы."
	minimap_icon = "engi"

/datum/job/som/squad/engineer/after_spawn(mob/living/carbon/new_mob, mob/user, latejoin = FALSE)
	. = ..()
	if(!ishuman(new_mob))
		return
	var/mob/living/carbon/human/new_human = new_mob
	var/playtime_mins = user?.client?.get_exp(title)
	if(!playtime_mins || playtime_mins < 1 )
		return
	switch(playtime_mins)
		if(0 to 1500) // starting
			new_human.wear_id.paygrade = "SOM_E3"
		if(1501 to 6000) // 25 hrs
			new_human.wear_id.paygrade = "SOM_E4"
		if(6001 to 18000) // 100 hrs
			new_human.wear_id.paygrade = "SOM_E5"
		if(18001 to 30000) // 300 hrs
			new_human.wear_id.paygrade = "SOM_S1"
		if(30001 to INFINITY) // 500 hrs
			new_human.wear_id.paygrade = "SOM_S2"

/datum/job/som/squad/engineer/radio_help_message(mob/M)
	. = ..()
	to_chat(M, {"\nYou拥有建造防御工事,改接电力线路和固守阵地的装备与技能.
在战场建设方面,你的小队队员会指望你."})

/datum/job/som/squad/medic
	paygrade = "SOM_E3"
	comm_title = "Med"
	total_positions = 16
	minimap_icon = "medic"
	skills_type = /datum/skills/combat_medic
	display_order = JOB_DISPLAY_ORDER_SQUAD_CORPSMAN
	outfit = /datum/outfit/job/som/squad/medic
	//job_flags = JOB_FLAG_LATEJOINABLE|JOB_FLAG_ROUNDSTARTJOINABLE|JOB_FLAG_ALLOWS_PREFS_GEAR|JOB_FLAG_PROVIDES_BANK_ACCOUNT|JOB_FLAG_ADDTOMANIFEST|JOB_FLAG_PROVIDES_SQUAD_HUD|JOB_FLAG_CAN_SEE_ORDERS
	jobworth = list(
		/datum/job/xenomorph = LARVA_POINTS_REGULAR,
		/datum/job/som/squad/veteran = VETERAN_POINTS_REGULAR,
	)
	html_description = {"
		<b>Difficulty</b>: Hard<br /><br />
		<b>You answer to the</b> acting Squad Leader<br /><br />
		<b>Unlock Requirement</b>: Starting Role<br /><br />
		<b>Gamemode Availability</b>: Combat patrol and Sensor Capture<br /><br /><br />
		Corpsman are the vital line between life and death of a marine’s life should a marine be wounded in battle, if provided they do not run away. While marines treat themselves, it is the corpsmen who will treat injuries beyond what a normal person can do. With a higher degree of medical skill compared to a normal marine, they are capable of doing medical actions faster and reviving with defibrillators will heal more on each attempt. They can also perform surgery, in an event if there are no acting medical officers onboard.
		<br /><br />
		<b>Duty</b>: Tend the injuries of your fellow marines or related personnel, keep them at fighting strength.
	"}
	job_desc = "Лечите ранения своих сослуживцев, поддерживая их боеспособность."

/datum/job/som/squad/medic/after_spawn(mob/living/carbon/new_mob, mob/user, latejoin = FALSE)
	. = ..()
	if(!ishuman(new_mob))
		return
	var/mob/living/carbon/human/new_human = new_mob
	var/playtime_mins = user?.client?.get_exp(title)
	if(!playtime_mins || playtime_mins < 1 )
		return
	switch(playtime_mins)
		if(0 to 1500) // starting
			new_human.wear_id.paygrade = "SOM_E3"
		if(1501 to 6000) // 25 hrs
			new_human.wear_id.paygrade = "SOM_E4"
		if(6001 to 18000) // 100 hrs
			new_human.wear_id.paygrade = "SOM_E5"
		if(18001 to 30000) // 300 hrs
			new_human.wear_id.paygrade = "SOM_S1"
		if(30001 to INFINITY) // 500 hrs
			new_human.wear_id.paygrade = "SOM_S2"

/datum/job/som/squad/medic/radio_help_message(mob/M)
	. = ..()
	to_chat(M, {"\nYou必须照料小队战友的伤口,确保他们健康且活跃.
你可能不是一名正式医生,但在关键时刻你站在生死之间."})

/datum/job/som/squad/veteran
	paygrade = "SOM_S1"
	comm_title = "Vet"
	total_positions = 8
	skills_type = /datum/skills/crafty //smarter than the average bear
	display_order = JOB_DISPLAY_ORDER_SQUAD_SMARTGUNNER
	minimap_icon = "smartgunner"
	outfit = /datum/outfit/job/som/squad/veteran
	//job_flags = JOB_FLAG_LATEJOINABLE|JOB_FLAG_ROUNDSTARTJOINABLE|JOB_FLAG_ALLOWS_PREFS_GEAR|JOB_FLAG_PROVIDES_BANK_ACCOUNT|JOB_FLAG_ADDTOMANIFEST|JOB_FLAG_PROVIDES_SQUAD_HUD|JOB_FLAG_CAN_SEE_ORDERS
	jobworth = list(/datum/job/xenomorph = LARVA_POINTS_REGULAR)
	html_description = {"
		<b>Difficulty</b>: Medium<br /><br />
		<b>You answer to the</b> acting Squad Leader<br /><br />
		<b>Unlock Requirement</b>: Starting Role<br /><br />
		<b>Gamemode Availability</b>: Combat patrol and Sensor Capture<br /><br /><br />
		You are a seasoned veteran of the SOM. You have fought and bled for the cause, proving your self a true Son of Mars. As fitting reward for your service, you are entrusted with best arms and equipment the SOM can offer, and you are expected to serve as an example to your fellow soldier.
		<br /><br />
		<b>Duty</b>: Show your comrades how a true Son of Mars acts, and crush our enemies without mercy!.
	"}
	job_desc = "Покажите товарищам, как действует настоящий Сын Марса, и без пощады сокрушайте врагов!"

/datum/job/som/squad/veteran/after_spawn(mob/living/carbon/new_mob, mob/user, latejoin = FALSE)
	. = ..()
	if(!ishuman(new_mob))
		return
	var/mob/living/carbon/human/new_human = new_mob
	var/playtime_mins = user?.client?.get_exp(title)
	if(!playtime_mins || playtime_mins < 1 )
		return
	switch(playtime_mins)
		if(0 to 1500) // starting
			new_human.wear_id.paygrade = "SOM_S1"
		if(1501 to 6000) // 25 hrs
			new_human.wear_id.paygrade = "SOM_S2"
		if(6001 to 18000) // 100 hrs
			new_human.wear_id.paygrade = "SOM_S3"
		if(18001 to 30000) // 300 hrs
			new_human.wear_id.paygrade = "SOM_S4"
		if(30001 to INFINITY) // 500 hrs
			new_human.wear_id.paygrade = "SOM_S5"

/datum/job/som/squad/veteran/radio_help_message(mob/M)
	. = ..()
	to_chat(M, {"\nYou是SOM中的一名老兵.凭借你的长期经验(以及更好的训练和装备),你的职责是提供特殊武器支援以巩固战线."})

/datum/job/som/squad/leader
	req_admin_notify = TRUE
	paygrade = "SOM_S3"
	comm_title = JOB_COMM_TITLE_SQUAD_LEADER
	total_positions = 4
	supervisors = "the acting field commander"
	minimap_icon = "leader"
	skills_type = /datum/skills/sl
	display_order = JOB_DISPLAY_ORDER_SQUAD_LEADER
	outfit = /datum/outfit/job/som/squad/leader
	exp_requirements = XP_REQ_INTERMEDIATE
	exp_type = EXP_TYPE_REGULAR_ALL
	//job_flags = JOB_FLAG_LATEJOINABLE|JOB_FLAG_ROUNDSTARTJOINABLE|JOB_FLAG_ALLOWS_PREFS_GEAR|JOB_FLAG_PROVIDES_BANK_ACCOUNT|JOB_FLAG_ADDTOMANIFEST|JOB_FLAG_BOLD_NAME_ON_SELECTION|JOB_FLAG_PROVIDES_SQUAD_HUD|JOB_FLAG_CAN_SEE_ORDERS
	jobworth = list(
		/datum/job/xenomorph = LARVA_POINTS_REGULAR,
		/datum/job/som/squad/veteran = VETERAN_POINTS_REGULAR,
	)
	html_description = {"
		<b>Difficulty</b>: Hard<br /><br />
		<b>You answer to the</b> acting Command Staff<br /><br />
		<b>Unlock Requirement</b>: Starting Role<br /><br />
		<b>Gamemode Availability</b>: Combat patrol and Sensor Capture<br /><br /><br />
		Squad Leaders are basically the boss of any able-bodied squad. Though while they are not trained compared to engineers, corpsmen and smartgunners, they are (usually) capable of leading the squad. They can issue orders to bolster their soldiers, and are expected to confidentally lead them to victory.
		<br /><br />
		<b>Duty</b>: Be a responsible leader of your squad, make sure your squad communicates frequently all the time and ensure they are working together for the task at hand. Stay safe, as you’re a valuable leader.
	"}
	job_desc = "Будьте ответственным лидером своего отряда, следите за постоянной связью между бойцами и слаженной работой над задачей. Берегите себя - вы ценный командир."

/datum/job/som/squad/leader/after_spawn(mob/living/carbon/C, mob/user, latejoin = FALSE)
	. = ..()
	if(!ishuman(C))
		return
	var/mob/living/carbon/human/new_human = C
	var/playtime_mins = user?.client?.get_exp(title)
	switch(playtime_mins)
		if(0 to 1500) // starting
			new_human.wear_id.paygrade = "SOM_S3"
		if(1501 to 6000) // 25 hrs
			new_human.wear_id.paygrade = "SOM_S4"
		if(6001 to 18000) // 100 hrs
			new_human.wear_id.paygrade = "SOM_S5"
		if(18001 to 30000) // 300 hrs
			new_human.wear_id.paygrade = "SOM_W1"
		if(30001 to INFINITY) // 500 hrs
			new_human.wear_id.paygrade = "SOM_W2"
	if(!latejoin)
		return
	if(!new_human.assigned_squad)
		return
	if(new_human.assigned_squad.squad_leader != new_human)
		if(new_human.assigned_squad.squad_leader)
			new_human.assigned_squad.demote_leader()
		new_human.assigned_squad.promote_leader(new_human)

/datum/job/som/squad/leader/radio_help_message(mob/M)
	. = ..()
	to_chat(M, {"\nYou负责你小队的男女队员.确保他们各司其职,协同合作并保持沟通.
你还负责与指挥部沟通,让他们第一时间了解情况.远离危险."})
