
/*
	Humans:
	Adds an exception for gloves, to allow special glove types like the ninja ones.

	Otherwise pretty standard.
*/

/mob/living/carbon/human/RestrainedClickOn(atom/A) //chewing your handcuffs
	if (A != src) return ..()
	var/mob/living/carbon/human/H = A

	if(TIMER_COOLDOWN_RUNNING(src, COOLDOWN_CHEW))
		to_chat(H, span_warning("你还不能再次咬自己的手..."))
		return

	if(!H.handcuffed)
		return
	if(H.a_intent != INTENT_HARM)
		return
	if(H.zone_selected != BODY_ZONE_PRECISE_MOUTH)
		return
	if(H.wear_mask)
		return
	if(istype(H.wear_suit, /obj/item/clothing/suit/straight_jacket))
		return

	var/datum/limb/O = H.get_limb(H.hand? BODY_ZONE_PRECISE_L_HAND : BODY_ZONE_PRECISE_R_HAND)
	if(!O)
		return

	var/s = span_warning("[H.name] 咬住了 [H.p_their()] [O.display_name]!")
	H.visible_message(s, span_warning("你咬住了自己的[O.display_name]!"))
	H.log_message("[s] ([key_name(H)])", LOG_ATTACK)

	if(O.take_damage_limb(1, 0, TRUE, TRUE))
		H.UpdateDamageIcon()

	TIMER_COOLDOWN_START(src, COOLDOWN_CHEW, 7.5 SECONDS)

/mob/living/carbon/human/UnarmedAttack(atom/A, proximity, list/modifiers)
	if(lying_angle) //No attacks while laying down
		return FALSE
	if(HAS_TRAIT(src, TRAIT_HANDS_BLOCKED))
		return

	var/obj/item/clothing/gloves/G = gloves // not typecast specifically enough in defines

	// Special glove functions:
	// If the gloves do anything, have them return TRUE to stop the touch
	// normal attack_hand() here.
	if(proximity && istype(G) && G.Touch(A, 1))
		return

	var/datum/limb/temp = get_limb(hand ? BODY_ZONE_PRECISE_L_HAND : BODY_ZONE_PRECISE_R_HAND)
	if(temp && !temp.is_usable())
		to_chat(src, span_notice("你试图移动你的[temp.display_name], 但无法做到!"))
		return

	if(LAZYACCESS(modifiers, "right"))
		A.attack_hand_alternate(src)
		SEND_SIGNAL(src, COMSIG_HUMAN_MELEE_UNARMED_ATTACK_ALTERNATE, A)
		return

	SEND_SIGNAL(src, COMSIG_HUMAN_MELEE_UNARMED_ATTACK, A)

	if(species?.spec_unarmedattack(src, A)) //Because species like monkeys dont use attack hand
		return
	A.attack_hand(src)
